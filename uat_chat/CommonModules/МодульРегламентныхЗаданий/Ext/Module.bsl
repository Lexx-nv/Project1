
// Процедура выполняет пересчет регистров накопления, для удаления нулевых записей
Процедура ПересчетИтоговРегистровНакопления() Экспорт

	НаДату = НачалоМесяца(ТекущаяДата())-1;	
	ПересчетРегистров(РегистрыНакопления, НаДату, Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки);
	
КонецПроцедуры

// Процедура выполняет пересчет регистров 
//
// Параметры:
//  МенеджерРегистров - менеджер регистров
//  НаДату       - дата пересчета
//  ОграничениеПоВидуРегистра - отбор регистров по виду
//
Процедура ПересчетРегистров(МенеджерРегистров, НаДату, ОграничениеПоВидуРегистра = Неопределено)
	
	Для Каждого МенеджерРегистра ИЗ МенеджерРегистров Цикл
		МетаданныеРегистра = Метаданные.НайтиПоТипу(ТипЗнч(МенеджерРегистра));
		
		Если ОграничениеПоВидуРегистра <> Неопределено И МетаданныеРегистра.ВидРегистра <> ОграничениеПоВидуРегистра Тогда
			Продолжить;
		КонецЕсли;
		ПересчитатьРегистрПоДате(МенеджерРегистра, НаДату);
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура выполняет пересчет регистров 
//
// Параметры:
//  МенеджерРегистра - менеджер регистра для пересчёта
//  НаДату       - дата пересчета
//
Процедура ПересчитатьРегистрПоДате(МенеджерРегистра, НаДату)
	
	Если МенеджерРегистра.ПолучитьПериодРассчитанныхИтогов()<НаДату Тогда
		МенеджерРегистра.УстановитьПериодРассчитанныхИтогов(НаДату);
	Иначе
		МенеджерРегистра.ПересчитатьИтоги();
	КонецЕсли;
	
КонецПроцедуры

