// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	СформироватьТаблицуТарифов(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицуТарифов(ДокументСсылка, СтруктураДополнительныеСвойства)
	тблТарифыЗП = Новый ТаблицаЗначений;
	тблТарифыЗП.Колонки.Добавить("Регистратор");
	тблТарифыЗП.Колонки.Добавить("Период");
	тблТарифыЗП.Колонки.Добавить("ТарифЗП");
	тблТарифыЗП.Колонки.Добавить("Сотрудник");
	тблТарифыЗП.Колонки.Добавить("ДатаНачала");
	тблТарифыЗП.Колонки.Добавить("Действует");
	
	Для Каждого ТекСтрокаТариф Из ДокументСсылка.Тарифы Цикл
		НоваяСтрока = тблТарифыЗП.Добавить();
		НоваяСтрока.Период = ТекСтрокаТариф.ДатаНачала;
		НоваяСтрока.Регистратор = ДокументСсылка;
		НоваяСтрока.ТарифЗП = ТекСтрокаТариф.Тариф;
		НоваяСтрока.Сотрудник = ТекСтрокаТариф.Сотрудник;
		НоваяСтрока.ДатаНачала = ТекСтрокаТариф.ДатаНачала;
		НоваяСтрока.Действует = ТекСтрокаТариф.Действует;
	КонецЦикла;
	
	//управляемая блокировка
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.уатТарифыСотрудников");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тблТарифыЗП;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТарифЗП", "ТарифЗП");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Сотрудник", "Сотрудник");
	Блокировка.Заблокировать();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТарифыЗП", тблТарифыЗП);
КонецПроцедуры // СформироватьТаблицуАгрегатыТС()

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылка, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	//Зарезервировано
КонецПроцедуры
