
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	мНаборЗаписейДополнительныеСведенияЗаявок = Движения.ДополнительныеСведенияЗаявок;
	мНаборЗаписейДополнительныеСведенияЗаявок.Записывать = Истина;
	
	мТаблицаСоставаЗаявки = Состав.Выгрузить();
	Для мИтр = 1 По День(КонецМесяца(Дата)) Цикл	//контроль количества дней в месяце - на совести формы документа
		Если Состав.Итог("д" + мИтр) > 0 Тогда	//время выполнения минимальное, но иногда ускорит проведение
			мДата = НачалоМесяца(Дата) + (86400 * (мИтр - 1));
			мКопияТаблицаСоставаЗаявки = мТаблицаСоставаЗаявки.Скопировать();
			//фактически, теперь можно не сворачивать, набор измерений и реквизитов полностью дублирует ТЧ, кроме СтрокаИсходныхДанных - которая в регистре нам не нужна
			//мКопияТаблицаСоставаЗаявки.Свернуть("ТипТС, ЦехМаршрут, ВремяПодачи, ХранилищеИдентификаторов, Ответственный, РежимРаботы, ЧасовОтстоя, СуточныйПробег, Комментарий, д" + мИтр, "КоличествоТС");
			
			Для Каждого мСтрока Из Состав Цикл
				Если мСтрока["д" + Число(мИтр)] И мСтрока.КоличествоТС > 0 И Не мСтрока.ИсключитьИзПроведения Тогда
					мМассивИдентификаторов = мСтрока.ХранилищеИдентификаторов.Получить();
					Для Итр = 0 По мМассивИдентификаторов.Количество() - 1 Цикл
						мНоваяЗапись = мНаборЗаписейДополнительныеСведенияЗаявок.Добавить();
						ЗаполнитьЗначенияСвойств(мНоваяЗапись, мСтрока);
						мНоваяЗапись.Регистратор = Ссылка;
						мНоваяЗапись.ИдентификаторСтрокиЗаявки = мМассивИдентификаторов[Итр];
						мНоваяЗапись.Период = мДата;
						мНоваяЗапись.ВремяПодачи = ?(Итр >= мСтрока.КоличествоТС, мСтрока.ВремяПодачи2см, мСтрока.ВремяПодачи);
						мНоваяЗапись.ВремяВозврата = ?(Итр >= мСтрока.КоличествоТС, мСтрока.ВремяВозврата2см, мСтрока.ВремяВозврата);
						мНоваяЗапись.Контрагент = Контрагент;
						мНоваяЗапись.КоличествоТС = 1;
						мНоваяЗапись.МестоРаботы = МестоРаботы;
						мНоваяЗапись.ИсходныеДанные = ?(ЗначениеЗаполнено(СокрЛП(мСтрока.СтрокаИсходныхДанных)), СтрЗаменить(СтрЗаменить(мСтрока.СтрокаИсходныхДанных, "&", " / "), "..", "--"), "");
						мНоваяЗапись.ИдентификаторСтрокиЛота = мСтрока.ИдентификаторСтрокиЛота;
						//закомментировано в связи с переходом на 1 регистр Разнарядка (без регистра с Уточнениями)
						////место оказания услуг задано явно
						//Если ЗначениеЗаполнено(мСтрока.МестоОказанияУслуг) Тогда
						//	мНоваяЗаписьУточнения = РегистрыСведений.Разнарядка.СоздатьМенеджерЗаписи();
						//	ЗаполнитьЗначенияСвойств(мНоваяЗаписьУточнения, мСтрока);
						//	мНоваяЗаписьУточнения.Дата = мДата;
						//	мНоваяЗаписьУточнения.ВремяПодачи = ?(Итр > мСтрока.КоличествоТС, мСтрока.ВремяПодачи2см, мСтрока.ВремяПодачи);
						//	мНоваяЗаписьУточнения.ИдентификаторСтрокиЗаявки = мМассивИдентификаторов[Итр];
						//	мНоваяЗаписьУточнения.Записать(Истина);
						//КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Дата = НачалоМесяца(Дата);
	Для Каждого мСтрока Из Состав Цикл
		мМассивИдентификаторовСтрок = мСтрока.ХранилищеИдентификаторов.Получить();
		Если мСтрока.СтрокаИсходныхДанных = "Очистить хранилище" Или Не ЗначениеЗаполнено(мМассивИдентификаторовСтрок)Тогда
			мМассивИдентификаторовСтрок = Новый Массив;
			Для Итр = 1 По мСтрока.КоличествоТС * ?(ЗначениеЗаполнено(мСтрока.ВремяПодачи2см), 2, 1) Цикл
				мМассивИдентификаторовСтрок.Добавить(СокрЛП(Новый УникальныйИдентификатор()));
			КонецЦикла;
			мСтрока.ХранилищеИдентификаторов = Новый ХранилищеЗначения(мМассивИдентификаторовСтрок);
			Если мСтрока.СтрокаИсходныхДанных = "Очистить хранилище" Тогда
				мСтрока.СтрокаИсходныхДанных = "";
			КонецЕсли;
		Иначе
			Если мМассивИдентификаторовСтрок.Количество() <> мСтрока.КоличествоТС * ?(ЗначениеЗаполнено(мСтрока.ВремяПодачи2см), 2, 1) Тогда
				Если мМассивИдентификаторовСтрок.Количество() > мСтрока.КоличествоТС * ?(ЗначениеЗаполнено(мСтрока.ВремяПодачи2см), 2, 1) Тогда
					мМассивУдаляемыхИдентификаторов = Новый Массив;
					Для Итр = 1 По мМассивИдентификаторовСтрок.Количество() - (мСтрока.КоличествоТС * ?(ЗначениеЗаполнено(мСтрока.ВремяПодачи2см), 2, 1)) Цикл
						//удаляем с конца, если перебор
						//TODO: Переписать для удаления с конца для обеих смен?... но добавление-то всегда в конец... Дилемма...
						мИндексУдаляемого = мМассивИдентификаторовСтрок.Количество() - 1;
						мМассивУдаляемыхИдентификаторов.Добавить(мМассивИдентификаторовСтрок[мИндексУдаляемого]);
						мМассивИдентификаторовСтрок.Удалить(мИндексУдаляемого);
					КонецЦикла;
					//TODO: ОбработкаУдаления(мМассивУдаляемыхИдентификаторов);
				Иначе
					Для Итр = 1 По (?(ЗначениеЗаполнено(мСтрока.ВремяПодачи2см), 2, 1) * мСтрока.КоличествоТС) - мМассивИдентификаторовСтрок.Количество() Цикл
						//добавляем, если недобор
						мМассивИдентификаторовСтрок.Добавить(СокрЛП(Новый УникальныйИдентификатор()));
					КонецЦикла;
				КонецЕсли;
				мСтрока.ХранилищеИдентификаторов = Новый ХранилищеЗначения(мМассивИдентификаторовСтрок);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Для Каждого мСтрока Из Состав Цикл
		мИндекс = Состав.Индекс(мСтрока);
		мСтрокаОбъектаКопирования = ОбъектКопирования.Состав.Получить(мИндекс);
		мСтрока.ХранилищеИдентификаторов = Новый ХранилищеЗначения(мСтрокаОбъектаКопирования.ХранилищеИдентификаторов.Получить());
	КонецЦикла;
	Дата = НачалоМесяца(КонецМесяца(ОбъектКопирования.Дата) + 1);
	ЭтотОбъект.Записать(РежимЗаписиДокумента.Запись);
КонецПроцедуры
