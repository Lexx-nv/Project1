////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт; // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения.
Перем таблТССсылка; // Хранит таблицу ТС, которая заполняется при инициализации документа из таблицы ТС
// по ссылке. Если при редактировании таблицы ТС она будет изменена, то все старые ТС при перепроведении
// должны возвратиться в исходное (до даты документа) местонахождение


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
	
	// Функция формирует простой табличный документ
	//
	// Возвращаемое значение:
	//  Табличный документ - печатная форма
	//
	Функция ПечатьВыбытие()
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВыбытиеТС";
		Макет = ПолучитьМакет("Выбытие");
		
		// Выводим шапку
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.ТекстЗаголовка = уатОбщегоНазначенияТиповые.уатСформироватьЗаголовокДокумента(ЭтотОбъект, "Выбытие ТС и оборудования");
		ТабДокумент.Вывести(ОбластьМакета);
		
		//формируем таблицы ТС и оборудования
		тблТС = ТС.Выгрузить().Скопировать();
		Сч = тблТС.Количество()-1;
		Пока Сч >= 0 Цикл
			Если НЕ тблТС[Сч].ТС.ВидМоделиТС = Перечисления.уатВидыМоделейТС.Автотранспорт Тогда
				тблТС.Удалить(Сч);
			КонецЕсли;
			Сч = Сч - 1;
		КонецЦикла;
		тблОборудование = ТС.Выгрузить().Скопировать();
		Сч = тблОборудование.Количество()-1;
		Пока Сч >= 0 Цикл
			Если тблОборудование[Сч].ТС.ВидМоделиТС = Перечисления.уатВидыМоделейТС.Автотранспорт Тогда
				тблОборудование.Удалить(Сч);
			КонецЕсли;
			Сч = Сч - 1;
		КонецЦикла;
		
		НомерСтроки = 1;
		// Выводим таблицу ТС
		Если тблТС.Количество() > 0 Тогда
			ТекОбласть = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабДокумент.Вывести(ТекОбласть);
			Для Каждого ТекСтрока Из тблТС Цикл
				ТекОбласть = Макет.ПолучитьОбласть("Строка");
				ТекОбласть.Параметры.Заполнить(ТекСтрока);
				ТекОбласть.Параметры.НомерСтроки = НомерСтроки;
				ТекОбласть.Параметры.ПредставлениеТС = уатОбщегоНазначения.уатПредставлениеТС(ТекСтрока.ТС, Организация);
				МестоТС = уатОбщегоНазначения.МестонахождениеТС(ТекСтрока.ТС, ТекСтрока.ДатаВыбытия-1);
				ТекОбласть.Параметры.Заполнить(МестоТС);
				НомерСтроки = НомерСтроки + 1;
				ТабДокумент.Вывести(ТекОбласть);
			КонецЦикла;
			
			ТекОбласть = Макет.ПолучитьОбласть("Итого");
			ТабДокумент.Вывести(ТекОбласть);
		КонецЕсли;
		
		НомерСтроки = 1;
		// Выводим таблицу оборудования
		Если тблОборудование.Количество() > 0 Тогда
			ТекОбласть = Макет.ПолучитьОбласть("ШапкаТаблицыОборудование");
			ТабДокумент.Вывести(ТекОбласть);
			Для Каждого ТекСтрока Из тблОборудование Цикл
				ТекОбласть = Макет.ПолучитьОбласть("СтрокаОборудование");
				ТекОбласть.Параметры.Заполнить(ТекСтрока);
				ТекОбласть.Параметры.НомерСтроки = НомерСтроки;
				ТекОбласть.Параметры.Оборудование = уатОбщегоНазначения.уатПредставлениеТС(ТекСтрока.ТС, Организация);
				МестоТС = уатОбщегоНазначения.МестонахождениеТС(ТекСтрока.ТС, ТекСтрока.ДатаВыбытия-1);
				ТекОбласть.Параметры.Заполнить(МестоТС);
				НомерСтроки = НомерСтроки + 1;
				ТабДокумент.Вывести(ТекОбласть);
			КонецЦикла;
			ТекОбласть = Макет.ПолучитьОбласть("ИтогоОборудование");
			ТабДокумент.Вывести(ТекОбласть);
		КонецЕсли;
		
		// Вывести подписи
		ТекОбласть = Макет.ПолучитьОбласть("Подвал");
		ТекОбласть.Параметры.Ответственный = Ответственный;
		ТабДокумент.Вывести(ТекОбласть);
		
		Возврат ТабДокумент;
		
	КонецФункции // ПечатьСписаниеТоваров()
	
	// Процедура осуществляет печать документа. Можно направить печать на 
	// экран или принтер, а также распечатать необходимое количество копий.
	//
	//  Название макета печати передается в качестве параметра,
	// по переданному названию находим имя макета в соответствии.
	//
	// Параметры:
	//  НазваниеМакета - строка, название макета.
	//
	Процедура Печать(НазваниеМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
		Если ЭтоНовый() Тогда
			Предупреждение("Документ можно распечатать только после его записи");
			Возврат;
		ИначеЕсли Не ?(Права = Неопределено, Ложь, уатПраваИНастройки.уатПраво("ПечатьНепроведенных", Права)) Тогда
			Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
			Возврат;
		КонецЕсли;
		
		Если Не уатОбщегоНазначенияТиповые.ПроверитьМодифицированность(ЭтотОбъект) Тогда
			Возврат;
		КонецЕсли;
		
		// Получить экземпляр документа на печать
		Если НазваниеМакета = "Выбытие" Тогда
			ТабДокумент = ПечатьВыбытие();
			уатОбщегоНазначенияТиповые.уатНапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, уатОбщегоНазначенияТиповые.уатСформироватьЗаголовокДокумента(ЭтотОбъект, ЭтотОбъект.Метаданные().Представление()), Права);
			
		ИначеЕсли ТипЗнч(НазваниеМакета) = Тип("СправочникСсылка.ВнешниеОбработки") Тогда
			
			ИмяФайла = КаталогВременныхФайлов()+"PrnForm.tmp";
			ОбъектВнешнейФормы = НазваниеМакета.ПолучитьОбъект();
			Если ОбъектВнешнейФормы = Неопределено Тогда
				Сообщить("Ошибка получения внешней формы документа. Возможно форма была удалена", СтатусСообщения.Важное);
				Возврат;
			КонецЕсли;			
			ДвоичныеДанные = ОбъектВнешнейФормы.ХранилищеВнешнейОбработки.Получить();
			ДвоичныеДанные.Записать(ИмяФайла);
			Обработка = ВнешниеОбработки.Создать(ИмяФайла);
			Попытка
				Обработка.СсылкаНаОбъект = Ссылка;
				ТабДокумент = Обработка.Печать();
				уатОбщегоНазначенияТиповые.уатНапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, уатОбщегоНазначенияТиповые.уатСформироватьЗаголовокДокумента(ЭтотОбъект, ЭтотОбъект.Метаданные().Представление()), Права);
			Исключение
				Сообщить("Ошибка формата внешней обработки. Возможно выбрана обработка не для печати.", СтатусСообщения.Важное);
			КонецПопытки;
			
		КонецЕсли;
		
		
		
	КонецПроцедуры // Печать
	
	// Возвращает доступные варианты печати документа
	//
	// Возвращаемое значение:
	//  Структура, каждая строка которой соответствует одному из вариантов печати
	//  
	//Функция ПолучитьСписокПечатныхФорм() Экспорт
	Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
		
		СтруктураМакетов = Новый Структура;
		
		СтруктураМакетов.Вставить("Выбытие", "Выбытие ТС и оборудования");
		
		Возврат СтруктураМакетов ;
		
	КонецФункции // ПолучитьСтруктуруПечатныхФорм()
	
	
#КонецЕсли


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Процедура ЗаписатьРеквизитыТСиОборудования(Отказ)
	Для каждого СтрТС Из ТС Цикл
		ТСОбъект = СтрТС.ТС.ПолучитьОбъект();
		ТСОбъект.ДатаВыбытия = СтрТС.ДатаВыбытия;
		
		ТСОбъект.Организация = Неопределено;
		ТСОбъект.Подразделение = Неопределено;
		ТСОбъект.Колонна = Неопределено;
		ТСОбъект.Состояние = справочники.уатСостояниеТС.Выбыло;
		
		Попытка 
			ТСОбъект.Записать();
			#Если Клиент Тогда
				Оповестить("ОбновитьФорму_МестонахождениеТС", , ТСОбъект.Ссылка);
			#КонецЕсли
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке("Не удалось записать ТС / оборудование """ + уатОбщегоНазначения.уатПредставлениеТС(СтрТС.ТС, Организация) + """", Отказ);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

Процедура ВосстановитьТСиОборудованиеДоВыбытия(Отказ)
	Для каждого СтрТС Из ТС Цикл
		ТСОбъект = СтрТС.ТС.ПолучитьОбъект();
		ТСОбъект.ДатаВыбытия = '00010101';
		
		МестоТС = уатОбщегоНазначения.МестонахождениеТС(СтрТС.ТС, СтрТС.ДатаВыбытия - 1);
		
		ТСОбъект.Организация = МестоТС.Организация;
		ТСОбъект.Подразделение = МестоТС.Подразделение;
		ТСОбъект.Колонна = МестоТС.Колонна;
		
		//Попытка 
			ТСОбъект.Записать();
			#Если Клиент Тогда
				Оповестить("ОбновитьФорму_МестонахождениеТС", , ТСОбъект.Ссылка);
			#КонецЕсли
		//Исключение
		//	ОбщегоНазначения.СообщитьОбОшибке("Не удалось записать ТС / оборудование """ + уатОбщегоНазначения.уатПредставлениеТС(СтрТС.ТС, Организация) + """", Отказ);
		//КонецПопытки;
	КонецЦикла;
КонецПроцедуры

Процедура ВосстановитьТСиОборудованиеПередЗаписью(Отказ)
	Для каждого ТекТС Из таблТССсылка Цикл
		ТСОбъект = ТекТС.ТС.ПолучитьОбъект();
		ТСОбъект.ДатаВыбытия = '00010101';
		
		МестоТС = уатОбщегоНазначения.МестонахождениеТС(ТекТС.ТС, ТекТС.ДатаВыбытия - 1);
		
		ТСОбъект.Организация = МестоТС.Организация;
		ТСОбъект.Подразделение = МестоТС.Подразделение;
		ТСОбъект.Колонна = МестоТС.Колонна;
		
		Попытка 
			ТСОбъект.Записать();
			#Если Клиент Тогда
				Оповестить("ОбновитьФорму_МестонахождениеТС", , ТСОбъект.Ссылка);
			#КонецЕсли
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке("Не удалось записать ТС / оборудование """ + уатОбщегоНазначения.уатПредставлениеТС(ТекТС.ТС, Организация) + """", Отказ);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

Функция ТСВведеныВЭксплуатацию(Отказ)
	Рез = Истина;
	
	Для Каждого ТекСтрока Из ТС Цикл
		Если ТекСтрока.ТС.ДатаВводаВЭксплуатацию = '00010101' ИЛИ ТекСтрока.ДатаВыбытия < ТекСтрока.ТС.ДатаВводаВЭксплуатацию Тогда
			ОбщегоНазначения.СообщитьОбОшибке("ТС / оборудование """ + уатОбщегоНазначения.уатПредставлениеТС(ТекСтрока.ТС, Организация) + """ не введено в эксплуатацию!", Отказ);
			Рез = Ложь;
		ИначеЕсли ТекСтрока.ТС.ДатаВыбытия <> '00010101' И ТекСтрока.ДатаВыбытия > ТекСтрока.ТС.ДатаВыбытия Тогда
			ОбщегоНазначения.СообщитьОбОшибке("ТС / оборудование """ + уатОбщегоНазначения.уатПредставлениеТС(ТекСтрока.ТС, Организация) + """ выбыло из эксплуатации (дата выбытия: " + Формат(ТекСтрока.ТС.ДатаВыбытия, "ДФ = дд.ММ.гггг") + ")!", Отказ);
			Рез = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Рез;
КонецФункции

Функция ТССоответствуютМестонахождению(Отказ)
	Рез = Истина;
	
	Для Каждого ТекСтрока Из ТС Цикл
		МестоТС = уатОбщегоНазначения.МестонахождениеТС(ТекСтрока.ТС, ТекСтрока.ДатаВыбытия - 1);
		Если МестоТС.Организация <> Организация ИЛИ МестоТС.Подразделение <> Подразделение ИЛИ МестоТС.Колонна <> Колонна Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Местонахождение ТС / оборудования """ + уатОбщегоНазначения.уатПредставлениеТС(ТекСтрока.ТС, Организация)
			+ """ не соответствует местонахождению по документу!", Отказ);
			Рез = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Рез;
КонецФункции


//////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.уатВыбытиеТСиОборудования.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	уатПроведение_проф.ОтразитьМестонахождениеТС(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль возникновения отрицательного остатка.
	Документы.уатВыбытиеТСиОборудования.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	// Запись даты ввода в карточках ТС и оборудования
	ЗаписатьРеквизитыТСиОборудования(Отказ);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	уатОбщегоНазначения.уатПроверкаПравПередЗаписьюВДокументах(ЭтотОбъект, Отказ, , Права);
	
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
		и ЗначениеЗаполнено(Проверил) ТОгда
		 Сообщить("Нельзя отменить  проведение у ПРОВЕРЕННЫХ докуметов!");
		 Отказ = Истина;
	КонецеслИ;
	
	//Проверка согласования документа
	Если (НЕ Отказ) И РежимЗаписи = РежимЗаписиДокумента.Проведение И ТребуетСогласования И (НЕ уатОбщегоНазначения_проф.ДокументСогласован(Ссылка)) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Документ """ + Ссылка + """ необходимо согласовать!", Отказ);
		Возврат;
	КонецЕсли;
	
	// Проверка на то, что все ТС и оборудование введены в эксплуатацию
	Если (НЕ Отказ) И РежимЗаписи = РежимЗаписиДокумента.Проведение И (НЕ ТСВведеныВЭксплуатацию(Отказ)) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка на то, что местонахождение ТС и оборудования
	//соответствует реквизитам шапки документа (если они указаны)
	Если (НЕ Отказ) И РежимЗаписи = РежимЗаписиДокумента.Проведение  Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВосстановитьТСиОборудованиеПередЗаписью(Отказ);
	КонецЕсли;
КонецПроцедуры // ПередЗаписью

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		таблТССсылка = Ссылка.ТС.Выгрузить();
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		// Заголовок для сообщений об ошибках проведения.
		Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ЭтотОбъект);
		
		СтруктураПолей = Новый Структура("Организация, Ответственный");
		уатОбщегоНазначенияТиповые.уатПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураПолей, Отказ, Заголовок);
		СтруктураПолей = Новый Структура("ТС, ДатаВыбытия");
		уатОбщегоНазначенияТиповые.уатПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ТС", СтруктураПолей, Отказ, Заголовок);
		
		//проверка на дубли строк
		тблТС = ТС.Выгрузить().Скопировать();
		тблТС.Свернуть("ТС");
		Если тблТС.Количество() < ТС.Количество() Тогда
			ОбщегоНазначения.СообщитьОбОшибке("В табличной части ""ТС"" присутствуют повторяющиеся строки (дублирование ТС)!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВосстановитьТСиОборудованиеДоВыбытия(Отказ);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПраваУАТ;
#КонецЕсли

таблТССсылка = Ссылка.ТС.Выгрузить();