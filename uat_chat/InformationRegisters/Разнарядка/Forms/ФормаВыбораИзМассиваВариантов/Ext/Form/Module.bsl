
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	мСтрокаМассиваВариантов = "";
	мРазделитель = "";
	Параметры.Свойство("СтрокаВариантов", мСтрокаМассиваВариантов);
	Параметры.Свойство("Разделитель", мРазделитель);
	Если мРазделитель = Неопределено Или мРазделитель = "" Тогда
		мРазделитель = ";";
	КонецЕсли;
	Если мСтрокаМассиваВариантов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если мСтрокаМассиваВариантов <> "" Тогда
		мМультиСтрока = СтрЗаменить(мСтрокаМассиваВариантов, мРазделитель, Символы.ПС);
		Для Итр = 1 По СтрЧислоСтрок(мМультиСтрока) Цикл
			//создаем команду и связываем с процедурой возврата индекса из имени
			мТекущаяСтрока = СокрЛП(СтрПолучитьСтроку(мМультиСтрока, Итр));
			мКоманда = ЭтаФорма.Команды.Добавить("" + СтрЗаменить(мТекущаяСтрока, " ", "") + "___" + Формат(Итр, "ЧГ="));
			мКоманда.Действие = "ОбработкаКомандыНажатиеКнопкиВарианта";
			мКоманда.Заголовок = Формат(Итр, "ЧГ=");
			
			//добавляем кнопку на форму
			мКнопка = Элементы.Добавить("Кнопка" + Формат(Итр, "ЧГ="), Тип("КнопкаФормы"), Элементы.ГруппаКнопок);
			мКнопка.ИмяКоманды = мКоманда.Имя;
			мКнопка.Заголовок = мТекущаяСтрока;
		КонецЦикла;
	Иначе
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыНажатиеКнопкиВарианта(Команда)
	мПозицияЧисла = Найти(Команда.Имя, "___");
	мСтруктураВозврата = Новый Структура("ВыбранныйИндекс", Число(Сред(Команда.Имя, мПозицияЧисла + 3)));
	ЭтаФорма.Закрыть(мСтруктураВозврата);
КонецПроцедуры