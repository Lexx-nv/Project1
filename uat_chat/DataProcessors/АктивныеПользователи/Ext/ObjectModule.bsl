Функция ПолучитьИмяОбъекта() Экспорт
	Возврат "ОбработкаОбъект." + ЭтотОбъект.Метаданные().Имя;
КонецФункции

Функция ПолучитьОбъектСохраненнойНастройки() Экспорт
	Перем ЭлементНастройкиОбъект;
	Если Метаданные.Справочники.Найти("СохраненныеНастройки") <> Неопределено Тогда
		ЭлементНастройки = Справочники.СохраненныеНастройки.НайтиПоРеквизиту("НастраиваемыйОбъект",ПолучитьИмяОбъекта());
		Если ЗначениеЗаполнено(ЭлементНастройки) Тогда
			ЭлементНастройкиОбъект = ЭлементНастройки.ПолучитьОбъект();
		Иначе
			ЭлементНастройкиОбъект = Справочники.СохраненныеНастройки.СоздатьЭлемент();
			Если Метаданные.Справочники.СохраненныеНастройки.Владельцы.Количество() > 0 Тогда
				Для каждого мтВладелец из Метаданные.Справочники.СохраненныеНастройки.Владельцы Цикл
					Если мтВладелец.ПолноеИмя() = "Справочник.ГруппыПользователей" Тогда
						ЭлементНастройкиОбъект.Владелец = Справочники.ГруппыПользователей.ВсеПользователи;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ЭлементНастройкиОбъект.НастраиваемыйОбъект = ПолучитьИмяОбъекта();
			Если Метаданные.Перечисления.ТипыНастроек.ЗначенияПеречисления.Найти("НастрокиФормы") <> Неопределено Тогда
				ЭлементНастройкиОбъект.ТипНастройки = Перечисления.ТипыНастроек.НастрокиФормы;
			Иначе
				ЭлементНастройкиОбъект.ТипНастройки = Перечисления.ТипыНастроек.НастройкиФормы;
			КонецЕсли;
			ЭлементНастройкиОбъект.Пользователи.Добавить().Пользователь = Справочники.ГруппыПользователей.ВсеПользователи;
			ЭлементНастройкиОбъект.Наименование = "Настройки обработки ""Активные пользователи""";
		КонецЕсли;
	ИначеЕсли Метаданные.РегистрыСведений.Найти("СохраненныеНастройки") <> Неопределено Тогда
		НаборЗаписей = РегистрыСведений.СохраненныеНастройки.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Значение = Справочники.Пользователи.ПустаяСсылка();
		НаборЗаписей.Отбор.Пользователь.Использование = Истина;
		НаборЗаписей.Отбор.ИмяОбъекта.Значение = ПолучитьИмяОбъекта();
		НаборЗаписей.Отбор.ИмяОбъекта.Использование = Истина;
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Пользователь = Справочники.Пользователи.ПустаяСсылка();
			ЗаписьНабора.ИмяОбъекта = ПолучитьИмяОбъекта();
			ЗаписьНабора.НаименованиеНастройки = "Настройки обработки ""Активные пользователи""";
		КонецЕсли;
		ЭлементНастройкиОбъект = НаборЗаписей;
	КонецЕсли;
	Возврат ЭлементНастройкиОбъект;
КонецФункции

Функция _ВосстановитьЗначение(КлючЭлементаСтруктуры) Экспорт
	Перем СохраненныеНастройки;
	ЭлементНастройкиОбъект = ПолучитьОбъектСохраненнойНастройки();
	ТипЗначенияЭлементаНастройки = ТипЗнч(ЭлементНастройкиОбъект);
	Если Лев(Строка(ТипЗначенияЭлементаНастройки),10) = "Справочник" Тогда
		СохраненныеНастройки = ЭлементНастройкиОбъект.ХранилищеНастроек.Получить();
	ИначеЕсли Лев(Строка(ТипЗначенияЭлементаНастройки),7) = "Регистр" Тогда
		СохраненныеНастройки = ЭлементНастройкиОбъект[0].СохраненнаяНастройка.Получить();
	КонецЕсли;
	Если ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
		Если СохраненныеНастройки.Свойство(КлючЭлементаСтруктуры) Тогда
			Возврат СохраненныеНастройки[КлючЭлементаСтруктуры];
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция _СохранитьЗначение(КлючЭлементаСтруктуры, ЗначениеЭлементыСтруктуры) Экспорт
	ЭлементНастройкиОбъект = ПолучитьОбъектСохраненнойНастройки();
	ТипЗначенияЭлементаНастройки = ТипЗнч(ЭлементНастройкиОбъект);
	Если Лев(Строка(ТипЗначенияЭлементаНастройки),10) = "Справочник" Тогда
		СохраненныеНастройки = ЭлементНастройкиОбъект.ХранилищеНастроек.Получить();
		Если ТипЗнч(СохраненныеНастройки) <> Тип("Структура") Тогда
			СохраненныеНастройки = Новый Структура();
		КонецЕсли;
		СохраненныеНастройки.Вставить(КлючЭлементаСтруктуры,ЗначениеЭлементыСтруктуры);
		ЭлементНастройкиОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(СохраненныеНастройки);
		Попытка
			ЭлементНастройкиОбъект.Записать();
			Возврат ИСТИНА;
		Исключение
			Сообщить("Ошибка записи настроек: "+ОписаниеОшибки(),СтатусСообщения.ОченьВажное);
		КонецПопытки;
	ИначеЕсли Лев(Строка(ТипЗначенияЭлементаНастройки),7) = "Регистр" Тогда
		СохраненныеНастройки = ЭлементНастройкиОбъект[0].СохраненнаяНастройка.Получить();
		Если ТипЗнч(СохраненныеНастройки) <> Тип("Структура") Тогда
			СохраненныеНастройки = Новый Структура();
		КонецЕсли;
		СохраненныеНастройки.Вставить(КлючЭлементаСтруктуры,ЗначениеЭлементыСтруктуры);
		ЭлементНастройкиОбъект[0].СохраненнаяНастройка = Новый ХранилищеЗначения(СохраненныеНастройки);
		Попытка
			ЭлементНастройкиОбъект.Записать();
			Возврат ИСТИНА;
		Исключение
			Сообщить("Ошибка записи настроек: "+ОписаниеОшибки(),СтатусСообщения.ОченьВажное);
		КонецПопытки;
	Иначе
		Сообщить("Не найден объект метаданных ""Справочник.СохраненныеНастройки"" или ""РегистрСведений.СохраненныеНастройки"" для записи настроек !",СтатусСообщения.ОченьВажное);
	КонецЕсли;
	Возврат ЛОЖЬ;
КонецФункции
