
Функция Данные(ЕстьВРС = Ложь,НачалоПериода,КонецПериода,СистемаМониторинга = "") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уатПутевойЛист.Ссылка Как ПутевойЛист,
		|	уатПутевойЛист.ТранспортноеСредство КАК ТранспортноеСредство,
		|	уатПутевойЛист.ТранспортноеСредство.ГаражныйНомер КАК ГаражныйНомер,
		|	уатПутевойЛист.ТранспортноеСредство.СистемаМониторинга КАК СистемаМониторинга,
		|	уатПутевойЛист.ДатаВыезда,
		|	уатПутевойЛист.ДатаВозвращения,
		|	ксДанныеАтографа.ПутевойЛист Как ПутевойЛистРС,
		|	ксДанныеАтографа.ДтНач Как ДатаВыездаРС,
		|	ксДанныеАтографа.ДтКон Как ДатаВозвращенияРС,
		|	ксДанныеАтографа.Лог Как ЛогРС,
		|	ксДанныеАтографа.ТранспортноеСредство Как ТСРС
		|ИЗ
		|	Документ.уатПутевойЛист КАК уатПутевойЛист
		|		Левое СОЕДИНЕНИЕ РегистрСведений.ксДанныеАтографа КАК ксДанныеАтографа
		|		ПО уатПутевойЛист.Ссылка = ксДанныеАтографа.ПутевойЛист
		|			И уатПутевойЛист.ТранспортноеСредство = ксДанныеАтографа.ТранспортноеСредство
		//Следующие 2 условия позволяют вычленить путевые листы, у которых были изменены дата/время выезда/заезда, а данные из БСМТС остались по прежним дате/времени
		//Данные БСМТС по таким путевым листам обновляем в обязательном порядке
		|			И уатПутевойЛист.ДатаВыезда = ксДанныеАтографа.ДтНач
		|			И уатПутевойЛист.ДатаВозвращения = ксДанныеАтографа.ДтКон
		|ГДЕ
		|	уатПутевойЛист.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И уатПутевойЛист.Проведен = ИСТИНА
		|	И НЕ уатПутевойЛист.ТранспортноеСредство.СистемаМониторинга = ЗНАЧЕНИЕ(Справочник.СистемыМониторинга.ПустаяСсылка)
		|	И ВЫБОР
		|			КОГДА &ОтборПоСистемеМониторинга
		|				ТОГДА уатПутевойЛист.ТранспортноеСредство.СистемаМониторинга = &ВыбСистемаМониторинга
		|			ИНАЧЕ ИСТИНА = ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &ЕстьВРС
		|				ТОГДА НЕ ксДанныеАтографа.ПутевойЛист ЕСТЬ NULL
		|			ИНАЧЕ ксДанныеАтографа.ПутевойЛист ЕСТЬ NULL
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	СистемаМониторинга,
		|	ТранспортноеСредство,
		|	ПутевойЛист.Дата
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
	Запрос.УстановитьПараметр("ОтборПоСистемеМониторинга", ЗначениеЗаполнено(СистемаМониторинга));
	Запрос.УстановитьПараметр("ВыбСистемаМониторинга", СистемаМониторинга);
	Запрос.УстановитьПараметр("ЕстьВРС", ЕстьВРС);
	
	//Для каждого пар из Запрос.Параметры Цикл
	//	ЗаписьЖурналаРегистрации("Тест",,,,""+Пар.Значение+" --- "+Пар.Ключ);
	//КонецЦикла;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции

// ТЗ - таблица значений, которую необходимо обработать
Процедура ПолучитьДанныеИзБСМТС(ТЗ,ФлагРегл = "") Экспорт
	
	
	Если ТЗ.Количество() <> 0 Тогда
		
		ТЗ.Сортировать("СистемаМониторинга");
		
		ТекСисМон = "Начало";
		
		Для Каждого ХХХ Из ТЗ Цикл
			
			Если  ХХХ.ТранспортноеСредство.СистемаМониторинга.ВидСистемыGPS = Перечисления.ВидСистемыGPS.Автограф Тогда
				ДатаС = ХХХ.ДатаВыезда;
				ДатаПо = ХХХ.ДатаВозвращения;
				ТекстОшибки = "";
				Рез = глСистемыМониторингаСервер.АвтоГРАФ5_ПолучитьДанныеДляПЛПоТС(ДатаС, ДатаПо, ХХХ.ТранспортноеСредство, ТекстОшибки, ХХХ.ТранспортноеСредство.СистемаМониторинга);
			ИначеЕсли ХХХ.ТранспортноеСредство.СистемаМониторинга.ВидСистемыGPS = Перечисления.ВидСистемыGPS.Виалон Тогда
				//Вот здесь пошел виалон и нам надо 1 раз получить ИДДанные
				Если ТекСисМон = "Начало" Тогда
					ИДДанные = глАвтограф.ПолучитьИДДанные(ХХХ.ТранспортноеСредство.СистемаМониторинга);
					ТекСисМон = "Виалон"
				КонецЕсли;	
				Рез = глАвтограф.ПолучитьДанныеПоТС(ХХХ.ТранспортноеСредство,ХХХ.ДатаВыезда,ХХХ.ДатаВозвращения,ИДДанные,Ложь);
			Иначе
				Продолжить;
			КонецЕсли;
			
			//Если мы получили структуру, то запишем данные в регистр сведений
			Если ТипЗнч(Рез) = Тип("Структура")  Тогда
				Зап = РегистрыСведений.ксДанныеАтографа.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Зап,Рез);
				Зап.ПутевойЛист = ХХХ.ПутевойЛист;
				Зап.Лог = Строка(ТекущаяДата()) + " - " + Строка(ИмяПользователя() + ФлагРегл);
				Зап.ДтНач = ХХХ.ДатаВыезда;
				Зап.ДтКон = ХХХ.ДатаВозвращения;
				Зап.ТранспортноеСредство = ХХХ.ТранспортноеСредство;
				//+Lexx - запишем в реквизит с типом "ХранилищеЗначений" весь перечень параметров из БСМТС
				Хранилище = Новый ХранилищеЗначения(Рез.ПараметрыБСМТС);
				Зап.ПараметрыБСМТС = Хранилище;
				Зап.Записать();
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ВыполнитьЗагрузку() Экспорт
	//Определим период, за который будем обрабатывать путевые листы:
	//с даты последнего закрытия ГСМ по сегодняшний день
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РасходГСМзаМесяц.Дата КАК ДатаЗакрытияГСМ
		|ИЗ
		|	Документ.РасходГСМзаМесяц КАК РасходГСМзаМесяц
		|ГДЕ
		|	РасходГСМзаМесяц.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И РасходГСМзаМесяц.Проведен = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасходГСМзаМесяц.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДобавитьМесяц(ТекущаяДата(),-6));
	Запрос.УстановитьПараметр("ДатаОкончания", ТекущаяДата());
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		//ТЗ = Обработки.ПолучениеДанныхИзБСМТС.Данные(Ложь,Выборка.ДатаЗакрытияГСМ,ТекущаяДата(),"");
		ТЗ = Данные(Ложь,Выборка.ДатаЗакрытияГСМ,ТекущаяДата(),"");
		ПолучитьДанныеИзБСМТС(ТЗ," - регл");
	КонецЕсли;	
КонецПроцедуры	



