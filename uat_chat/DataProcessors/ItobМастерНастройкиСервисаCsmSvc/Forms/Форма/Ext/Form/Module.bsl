
// Функция возвращает адрес инсталлятора во временном хранилище
//
&НаСервере
Функция ПоместитьИнсталляторВХранилище()
	
	Макет = ПолучитьОбщийМакет("ItobCsmSvcSetup");
	
	ИмяФайла = КаталогВременныхФайлов() + "ItobCsmSvcSetup.exe";
	
	Макет.Записать(ИмяФайла);
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
	
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные, Новый УникальныйИдентификатор());	
	
КонецФункции // ПоместитьИнсталляторВХранилище()

// Процедура запускает инсталлятор службы CsmSvc.
//
&НаКлиенте
Процедура УстановитьСлужбуCsmSvc()
	
	#Если НЕ ВебКлиент Тогда
	
	Адрес = ПоместитьИнсталляторВХранилище();
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ДвоичныеДанные.Записать(КаталогВременныхФайлов() + "ItobCsmSvcSetup.exe");
	
	ЗапуститьПриложение(КаталогВременныхФайлов() + "ItobCsmSvcSetup.exe", , Истина);
	
	#КонецЕсли
	
КонецПроцедуры // УстановитьСлужбуCsmSvc()

// Процедура активизирует форму инсталлятора службы CsmSvc.
//
&НаКлиенте
Процедура АктивизироватьОкноФормы(ТекстПредупреждения)
	
	#Если НЕ ВебКлиент Тогда
	
	// Взято с http://www.forum.mista.ru/topic.php?id=416922
	
	WSS=Новый COMОбъект("WScript.Shell");
	oExec=WSS.Exec("cmd /k");
	QueryResult=ПолучитьCOMОбъект("winmgmts:\\.\root\CIMV2").ExecQuery("SELECT ParentProcessId FROM Win32_Process where ProcessId = '"+Формат(oExec.ProcessID,"ЧГ=0")+"'");
	Для Каждого Item Из QueryResult Цикл 
		CurrentProcessId=Item.ParentProcessId;
	КонецЦикла;
	oExec.Terminate();
	jsName=КаталогВременныхФайлов()+(Новый УникальныйИдентификатор)+".js";
	Скрипт=Новый ТекстовыйДокумент;
	Скрипт.ДобавитьСтроку("WScript.CreateObject(""WScript.Shell"").AppActivate("+Формат(CurrentProcessId,"ЧГ=0")+");
	|(new ActiveXObject(""Scripting.FileSystemObject"")).DeleteFile("""+СтрЗаменить(jsName,"\","\\")+""");");
	Скрипт.Записать(jsName,"windows-1251");
	WSS.Run(""""+jsName+"""",0);
	
	Предупреждение(ТекстПредупреждения,1);
	
	#КонецЕсли
	
КонецПроцедуры // АктивизироватьОкноФормы()

// Процедура - обработчик команды "Далее"
//
&НаКлиенте
Процедура КомандаДалее(Команда)
	
	Если Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаСтарт" Тогда
		
		Если РасположениеСлужбы = 0 Тогда
			
			СисИнфо = Новый СистемнаяИнформация;
			
			Если СисИнфо.ТипПлатформы = ТипПлатформы.Linux_x86 ИЛИ СисИнфо.ТипПлатформы = ТипПлатформы.Linux_x86_64 Тогда
				
				Предупреждение("Службу CsmSvc необходимо инсталлировать на операционной системе Windows");
				Возврат;
				
			КонецЕсли;
			
			Элементы.КнопкаДалее.Заголовок = "Установить службу CsmSvc >>";
			Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы["СтраницаЛокальнаяУстановка1"];			
			
			
		Иначе
			
			ЗначениеКонстантыАдресСервиса = ПолучитьКонстантуАдресСервиса();
			ЗначениеКонстантыАдресСервиса = СтрЗаменить(ЗначениеКонстантыАдресСервиса, ":", Символы.ПС);
			
			АдресСервера = СтрПолучитьСтроку(ЗначениеКонстантыАдресСервиса,1);
			Порт = СтрПолучитьСтроку(ЗначениеКонстантыАдресСервиса,2);
			
			Элементы.КнопкаДалее.Заголовок = "Готово";
			Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы["СтраницаСетеваяУстановка1"];		
			
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаЛокальнаяУстановка1" Тогда
		
		УстановитьСлужбуCsmSvc();
		
		#Если НЕ ВебКлиент Тогда
			
		Попытка
				
			ИмяФайла = КаталогВременныхФайлов() + "ipconfig.txt";
			
			WshShell = ПолучитьCOMОбъект("", "WScript.Shell");
			WshShell.Run("cmd /c ipconfig /all >> "+ИмяФайла, 2, Истина);
			
			// Активизируем окно 1С:Предприятия
			АктивизироватьОкноФормы("Сбор данных по сетевым интерфейсам...");	
			
			// Сбор данных по сетевым интерфейсам
			ТекстовыйДок = Новый ТекстовыйДокумент;
			ТекстовыйДок.Прочитать(ИмяФайла);
			
			СписокIP = Новый СписокЗначений;
			
			Для п = 1 По ТекстовыйДок.КоличествоСтрок() Цикл
				ТекСтрока = ВРег(СокрЛП(ТекстовыйДок.ПолучитьСтроку(п)));
				Если Лев(ТекСтрока,СтрДлина("IP Address")) = "IP ADDRESS" Тогда
					ТекущийIP = СокрЛП(Сред(ТекСтрока, Найти(ТекСтрока,":")+1));
					
					Если СписокIP.НайтиПоЗначению(ТекущийIP) = Неопределено Тогда
						СписокIP.Добавить(ТекущийIP);	
						
					КонецЕсли;
					
				КонецЕсли;			
				
			КонецЦикла;
			
			СписокIP.СортироватьПоЗначению();
			
			Если СписокIP.НайтиПоЗначению("127.0.0.1") = Неопределено Тогда
				СписокIP.Вставить(0, "127.0.0.1");
				
			КонецЕсли;
			
			Для каждого ЭлементСписка Из СписокIP Цикл
				Элементы.СетевойИнтерфейс.СписокВыбора.Добавить(ЭлементСписка.Значение);
				
			КонецЦикла;
			
			СетевойИнтерфейс = "127.0.0.1";
			
			// Определим каталог установки программы
			ПутьКпрограмме = WshShell.RegRead("HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services\CsmService\ImagePath");
			// Сообщить(ПутьКпрограмме);
			ПутьКпрограмме = СокрЛП(ПутьКпрограмме) + "###";
			
			ПутьIniФайла = СтрЗаменить(ПутьКпрограмме,"CsmSvc.exe###", "CsmSvc.ini");
			
			ТекстовыйДок = Новый ТекстовыйДокумент;
			ТекстовыйДок.Прочитать(ПутьIniФайла);
			
			ТекСекция = "";
			
			СоответствиеПараметров = Новый Соответствие;
			СоответствиеПараметров.Вставить("ENABLED", "ИспользоватьРепликацию");
			СоответствиеПараметров.Вставить("SERVER", "АдресСервераРепликации");
			СоответствиеПараметров.Вставить("PORT", "ПортСервераРепликации");
			СоответствиеПараметров.Вставить("LOGIN", "ЛогинРепликации");
			СоответствиеПараметров.Вставить("PASSWORD", "ПарольРепликации");		
			
			Для п = 1 По ТекстовыйДок.КоличествоСтрок() Цикл
				
				ТекСтрока = СокрЛП(ТекстовыйДок.ПолучитьСтроку(п));
				ТекСтрокаВРег = ВРЕГ(ТекСтрока);
				
				Если ТекСтрокаВРег = "[HTTP]" Тогда
					ТекСекция = "http";
					
				ИначеЕсли ТекСтрокаВРег = "[REPLICATION]" Тогда
					ТекСекция = "replication";				
					
				КонецЕсли;
				
				Если ТекСекция = "http" Тогда
					
					Если Лев(ТекСтрокаВРег,4) = "PORT" Тогда
						Порт = Сред(ТекСтрока, Найти(ТекСтрока,"=")+1);				
						
					КонецЕсли;
					
				ИначеЕсли ТекСекция = "replication" Тогда
					
					Если ПустаяСтрока(ТекСтрока) ИЛИ Лев(ТекСтрока,1)=";" ИЛИ Найти(ТекСтрока,"=")=0 Тогда
						Продолжить;
						
					КонецЕсли;
					
					Для каждого ЭлементПараметров Из СоответствиеПараметров Цикл
						Если Лев(ТекСтрокаВРег,СтрДлина(ЭлементПараметров.Ключ)) = ЭлементПараметров.Ключ Тогда
							ЭтаФорма[ЭлементПараметров.Значение] = Сред(ТекСтрока, Найти(ТекСтрока,"=")+1);
							
						КонецЕсли;
						
					КонецЦикла;			
					
				КонецЕсли;			
				
			КонецЦикла;
		
		Исключение
			
			Предупреждение("Ошибка установки: "+ОписаниеОшибки()+Символы.ПС+"Требуются права локального администратора!", 60);
		
		КонецПопытки;	
		
		#КонецЕсли
		
		Элементы.КнопкаДалее.Заголовок = "Готово";
		Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы["СтраницаЛокальнаяУстановка2"];
		УправлениеВидимостьюНастройкаРепликации();
	
		
	ИначеЕсли Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаЛокальнаяУстановка2" Тогда
		
		Попытка
		
			WshShell = ПолучитьCOMОбъект("", "WScript.Shell");	
			
			WshShell.Run("net stop CsmService", 2, Истина);
			
			// Определим каталог установки программы
			ПутьКпрограмме = WshShell.RegRead("HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services\CsmService\ImagePath");
			// Сообщить(ПутьКпрограмме);
			ПутьКпрограмме = СокрЛП(ПутьКпрограмме) + "###";
			
			ПутьIniФайла = СтрЗаменить(ПутьКпрограмме,"CsmSvc.exe###", "CsmSvc.ini");
			
			ТекстовыйДок = Новый ТекстовыйДокумент;
			ТекстовыйДок.Прочитать(ПутьIniФайла);
			
			ТекСекция = "";
			
			СоответствиеПараметров = Новый Соответствие;
			СоответствиеПараметров.Вставить("ENABLED", "ИспользоватьРепликацию");
			СоответствиеПараметров.Вставить("SERVER", "АдресСервераРепликации");
			СоответствиеПараметров.Вставить("PORT", "ПортСервераРепликации");
			СоответствиеПараметров.Вставить("LOGIN", "ЛогинРепликации");
			СоответствиеПараметров.Вставить("PASSWORD", "ПарольРепликации");		
			
			Для п = 1 По ТекстовыйДок.КоличествоСтрок() Цикл
				
				ТекСтрока = СокрЛП(ТекстовыйДок.ПолучитьСтроку(п));
				ТекСтрокаВРег = ВРЕГ(ТекСтрока);
				
				Если ТекСтрокаВРег = "[HTTP]" Тогда
					
					ТекСекция = "http";
					
				ИначеЕсли ТекСтрокаВРег = "[REPLICATION]" Тогда
					ТекСекция = "replication";				
					
				КонецЕсли;
				
				Если ТекСекция = "http" Тогда
					
					Если Лев(ТекСтрокаВРег,4) = "PORT" Тогда
						ТекстовыйДок.ЗаменитьСтроку(п,"port="+Формат(Порт,"ЧГ=0")); 
					КонецЕсли;
					
				ИначеЕсли ТекСекция = "replication" Тогда
					
					Если ПустаяСтрока(ТекСтрока) ИЛИ Лев(ТекСтрока,1)=";" ИЛИ Найти(ТекСтрока,"=")=0 Тогда
						Продолжить;
						
					КонецЕсли;
					
					Для каждого ЭлементПараметров Из СоответствиеПараметров Цикл
						Если Лев(ТекСтрокаВРег,СтрДлина(ЭлементПараметров.Ключ)) = ЭлементПараметров.Ключ Тогда
							
							ЗначениеПараметра = ЭтаФорма[ЭлементПараметров.Значение];
							Если ТипЗнч(ЗначениеПараметра) = Тип("Число") Тогда
								ЗначениеПараметра = Формат(ЗначениеПараметра,"ЧГ=0");
							ИначеЕсли ТипЗнч(ЗначениеПараметра) = Тип("Булево") Тогда		
								ЗначениеПараметра = ?(ЗначениеПараметра=Истина, "1", "0");
							КонецЕсли;
							
							ТекстовыйДок.ЗаменитьСтроку(п, НРег(ЭлементПараметров.Ключ)+"="+ЗначениеПараметра); 
							
						КонецЕсли;
						
					КонецЦикла;			
					
				КонецЕсли;
			КонецЦикла;
			
			ТекстовыйДок.Записать(ПутьIniФайла);		
		
			WshShell.Run("net start CsmService", 2, Истина);
			
		Исключение
			
			Предупреждение("Ошибка: "+ОписаниеОшибки()+Символы.ПС+"Требуются права локального администратора!", 60);
		
		КонецПопытки;	
		
		АктивизироватьОкноФормы("Преверка доступности сервиса...");
		
		Если НЕ ItobОперативныйМониторингКлиент.ПроверитьДоступностьСервисаCsmSvc(СетевойИнтерфейс+":"+Формат(Порт,"ЧН=0; ЧГ=0")) Тогда
			Предупреждение("Сервис недоступен!");
			
		Иначе
			ПолныйАдресСервиса = СетевойИнтерфейс+":"+Формат(Порт,"ЧН=0; ЧГ=0");
			УстановитьКонстантуАдресСервиса(ПолныйАдресСервиса);
			ЭтаФорма.Закрыть(Истина);
			
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаСетеваяУстановка1" Тогда
		
		Если НЕ ItobОперативныйМониторингКлиент.ПроверитьДоступностьСервисаCsmSvc(АдресСервера+":"+Формат(Порт,"ЧН=0; ЧГ=0")) Тогда
			Предупреждение("Сервис недоступен!");
			
		Иначе
			ПолныйАдресСервиса = АдресСервера+":"+Формат(Порт,"ЧН=0; ЧГ=0");
			УстановитьКонстантуАдресСервиса(ПолныйАдресСервиса);
			ЭтаФорма.Закрыть(Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура устанавливает новое значение в константу ItobАдресСервисаCsmSvc.
//
&НаСервере
Процедура УстановитьКонстантуАдресСервиса(Адрес)
	
	Константы.ItobАдресСервисаCsmSvc.Установить(Адрес);
	
КонецПроцедуры // УстановитьКонстантуАдресСервиса()

// Функция возвращает значение константы ItobАдресСервисаCsmSvc.
//
&НаСервере
Функция ПолучитьКонстантуАдресСервиса()
	
	Возврат Константы.ItobАдресСервисаCsmSvc.Получить();
	
КонецФункции // ПолучитьКонстантуАдресСервиса()

// Устанавливает видимость элементов.
//
&НаКлиенте
Процедура УправлениеВидимостьюНастройкаРепликации()
	
	Элементы.АдресСервераРепликации.Доступность = ИспользоватьРепликацию;
	Элементы.ПортСервераРепликации.Доступность  = ИспользоватьРепликацию;
	Элементы.ЛогинРепликации.Доступность        = ИспользоватьРепликацию;
	Элементы.ПарольРепликации.Доступность       = ИспользоватьРепликацию; 
	
КонецПроцедуры // УправлениеВидимостьюНастройкаРепликации()

// Процедура - обработчик события "ПриИзменении" флага "ИспользоватьРепликацию".
//
&НаКлиенте
Процедура ИспользоватьРепликациюПриИзменении(Элемент)
	
	УправлениеВидимостьюНастройкаРепликации();	
	
КонецПроцедуры // ИспользоватьРепликациюПриИзменении()

// Процедура - обработчик события "ПриСозданииНаСервере".
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	#Если ВебКлиент Тогда
		
		Сообщить("Мастер настройки не работает в Web-клиенте!");
		Отказ = Истина;		
		
	#КонецЕсли	
	
КонецПроцедуры
