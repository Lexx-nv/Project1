#Если Клиент Тогда
	
Перем мТекстЗапросаОтборНоменклатурыПоРодителю;

//====================================================================================
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМЫ ВВОДА ДОПОЛНИТЕЛЬНЫХ ПАРАМЕТРОВ

// Процедура формирует и выводит сумму в информационной надписи.
//
Процедура мОбновитьНадписьФормулаСумма(Форма) Экспорт

	Если Форма.ЭлементыФормы.Найти("ТаблицаХарактеристикНоменклатуры") = Неопределено Тогда
		Форма.ЭлементыФормы.НадписьФормулаСумма.Заголовок = уатОбщегоНазначенияТиповые.уатФорматСумм(Форма.Цена * Форма.Количество,,"0,00");
	Иначе
		ИтоговаяСумма = 0;
		Для Каждого СтрокаТабличнойЧасти Из Форма.ТаблицаХарактеристикНоменклатуры Цикл
			ИтоговаяСумма = ИтоговаяСумма + Ценообразование.ПересчитатьЦенуПриИзмененииВалюты(СтрокаТабличнойЧасти.Цена, СтрокаТабличнойЧасти.ВалютаЦены, Форма.ВалютаЦены) * СтрокаТабличнойЧасти.Количество;
		КонецЦикла;
		Форма.ЭлементыФормы.НадписьФормулаСумма.Заголовок = ИтоговаяСумма;
	КонецЕсли;

КонецПроцедуры // мОбновитьНадписьФормулаСумма()

// Процедура возвращает в форму-владельца выбранные значения.
//
Процедура мВыборВозврат(Форма) Экспорт

	Если Форма.ЭлементыФормы.Найти("ТаблицаХарактеристикНоменклатуры") <> Неопределено Тогда

		МассивСтруктурПараметров = Новый Массив;

		Для Каждого СтрокаТаблицыХарактеристик Из Форма.ТаблицаХарактеристикНоменклатуры Цикл

			Если СтрокаТаблицыХарактеристик.Количество <> 0 Тогда

				СтруктураПараметров = Новый Структура();
				СтруктураПараметров.Вставить("ЕдиницаИзмерения", СтрокаТаблицыХарактеристик.ЕдиницаИзмерения);
				СтруктураПараметров.Вставить("Количество",       СтрокаТаблицыХарактеристик.Количество);
				СтруктураПараметров.Вставить("Цена",             СтрокаТаблицыХарактеристик.Цена);
				СтруктураПараметров.Вставить("ВалютаЦены",       СтрокаТаблицыХарактеристик.ВалютаЦены);

				МассивСтруктурПараметров.Добавить(СтруктураПараметров);
			КонецЕсли;

		КонецЦикла;

		Форма.Закрыть(МассивСтруктурПараметров);
		Возврат;

	КонецЕсли;

	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("ЕдиницаИзмерения", Форма.ЕдиницаИзмерения);
	СтруктураПараметров.Вставить("Количество",       Форма.Количество);
	СтруктураПараметров.Вставить("Цена",             Форма.Цена);

	Форма.Закрыть(СтруктураПараметров);

КонецПроцедуры // мВыборВозврат()

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура мПриОткрытии(Форма) Экспорт

	Перем ЗапрашиватьЦену;
	Перем ЕстьЦена, ЕстьКоличество;

	Форма.Заголовок = "Количество и цена """ + Форма.Номенклатура + """";

	ЗапрашиватьКоличество = Форма.ВладелецФормы.ЗапрашиватьКоличество;
	ЗапрашиватьЦену       = Форма.ВладелецФормы.ЗапрашиватьЦену;

	Если Форма.ЭлементыФормы.Найти("ТаблицаХарактеристикНоменклатуры") = Неопределено Тогда
		Форма.ЭлементыФормы.НадписьКоличество. Доступность = ЗапрашиватьКоличество;
		Форма.ЭлементыФормы.Количество.        Доступность = ЗапрашиватьКоличество;
		Форма.ЭлементыФормы.НадписьЦена.       Доступность = ЗапрашиватьЦену;
		Форма.ЭлементыФормы.Цена.              Доступность = ЗапрашиватьЦену;
		Форма.ЭлементыФормы.НадписьВалютаЦены. Доступность = ЗапрашиватьЦену;
	Иначе

		Если ЗапрашиватьЦену Тогда
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Найти("Цена").ПропускатьПриВводе = Ложь;
		Иначе
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Найти("Цена").ПропускатьПриВводе = Истина;
		КонецЕсли;

		// Активизируем первую строку
		Если Форма.ТаблицаХарактеристикНоменклатуры.Количество() > 0 Тогда
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.ТекущаяСтрока = Форма.ТаблицаХарактеристикНоменклатуры.Получить(0);
		КонецЕсли;

		Если СтруктураИсходныхПараметров.Свойство("ЕстьКоличество" , ЕстьКоличество) И НЕ ЕстьКоличество Тогда
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Количество.Данные       = "";
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Количество.ДанныеФлажка = "Количество";
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Количество.ТекстШапки   = "Подбирать в документ";
		КонецЕсли;

		Если Не СтруктураИсходныхПараметров.Свойство("ЕстьЦена" , ЕстьЦена) Или НЕ ЕстьЦена Тогда
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Цена.Видимость       = Ложь;
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.ВалютаЦены.Видимость = Ложь;
		Иначе
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Цена.Видимость       = Истина;
			Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.ВалютаЦены.Видимость = Истина;
			Если ЗапрашиватьЦену Тогда
				Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Цена.Доступность       = Истина;
				Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.ВалютаЦены.Доступность = Истина;
			Иначе
				Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.Цена.Доступность       = Ложь;
				Форма.ЭлементыФормы.ТаблицаХарактеристикНоменклатуры.Колонки.ВалютаЦены.Доступность = Ложь;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

	Форма.ЭлементыФормы.НадписьВалютаСуммы.Доступность = ЗапрашиватьЦену;
	Форма.ЭлементыФормы.НадписьСумма.      Доступность = ЗапрашиватьЦену;
	Форма.ЭлементыФормы.НадписьФормулаСумма.Доступность = ЗапрашиватьЦену;

	мОбновитьНадписьФормулаСумма(Форма);

КонецПроцедуры // мПриОткрытии()

// Процедура - обработчик события "ОбновлениеОтображения" формы.
//
Процедура мОбновлениеОтображения(Форма) Экспорт

КонецПроцедуры // мОбновлениеОтображения()

// Процедура - обработчик события "ПриИзменении" поля ввода количества.
//
Процедура мКоличествоПриИзменении(Форма, Элемент) Экспорт

	мОбновитьНадписьФормулаСумма(Форма);

КонецПроцедуры // мКоличествоПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода цены.
//
Процедура мЦенаПриИзменении(Форма, Элемент) Экспорт

	мОбновитьНадписьФормулаСумма(Форма);

КонецПроцедуры // мЦенаПриИзменении()

// Процедура - обработчик события "Нажатие" кнопки "ОК".
//
Процедура мКнопкаОКНажатие(Форма, Элемент) Экспорт

	мВыборВозврат(Форма);

КонецПроцедуры // мКнопкаОКНажатие()

// Процедура - обработчик события "ПриИзменении" поля единицы измерения.
//
Процедура мЕдиницаИзмеренияПриИзменении(Форма, Элемент) Экспорт

	мОбновитьНадписьФормулаСумма(Форма);

КонецПроцедуры // мЕдиницаИзмеренияПриИзменении()

мТекстЗапросаОтборНоменклатурыПоРодителю = "
|Номенклатура В (ВЫБРАТЬ Номенклатура.Ссылка ИЗ Справочник.Номенклатура
|	                             КАК Номенклатура ГДЕ Номенклатура.Родитель = &Родитель)";

#КонецЕсли