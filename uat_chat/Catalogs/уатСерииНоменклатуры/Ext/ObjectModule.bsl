////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем Права Экспорт; // Переменная объекта - ссылка на коллекцию прав, настроек и переменных окружения


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция устанавливает новое наименование серии по значениям реквизитов.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Строка - сформированное наименование.
//
Функция СформироватьНаименование() Экспорт
	ТаблицаНастроек = Константы.уатНастройкаФормированияНаименованийАгрегатов.Получить().Получить();
	Если ТаблицаНастроек = Неопределено ИЛИ НЕ ТаблицаНастроек.Количество() Тогда		
		СтрокаНаименование = СформироватьНаименованиеОбычное();
	Иначе
		СтрокаНаименование = СформироватьНаименованиеПоНастройкам();
	КонецЕсли;	
	
	Возврат СтрокаНаименование;
КонецФункции

Функция СформироватьНаименованиеПоНастройкам() Экспорт
	ТаблицаНастроек = Константы.уатНастройкаФормированияНаименованийАгрегатов.Получить().Получить();
	Если ТаблицаНастроек = Неопределено ИЛИ НЕ ТаблицаНастроек.Количество() Тогда
		Возврат "";
	КонецЕсли;
	
	ПредставлениеНаименования = "";
	ЕстьЗаполненныеПоля = Ложь;
	Для каждого ТекНастройка ИЗ ТаблицаНастроек цикл
		Попытка
			Если ТекНастройка.Поле = "[ГоденДо]" Тогда
				Если ТипАгрегата = Справочники.уатТипыАгрегатов.Шина Тогда
					Продолжить;
				КонецЕсли;
				мСтрока = Формат(ЭтотОбъект[СтрЗаменить(СтрЗаменить(ТекНастройка.Поле,"[",""),"]","")], "ДФ=dd.MM.yyyy");
			Иначе
				мСтрока = ЭтотОбъект[СтрЗаменить(СтрЗаменить(ТекНастройка.Поле,"[",""),"]","")];
			КонецЕсли;
			Если ЗначениеЗаполнено(мСтрока) тогда
				ЕстьЗаполненныеПоля = Истина;
			КонецЕсли;
		Исключение
			мСтрока = ТекНастройка.Поле;
		КонецПопытки;
		ПредставлениеНаименования = ПредставлениеНаименования + мСтрока;
	КонецЦикла;
	Если ЕстьЗаполненныеПоля тогда
		Возврат ПредставлениеНаименования;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

Функция СформироватьНаименованиеОбычное() Экспорт
	Если ТипАгрегата = Справочники.уатТипыАгрегатов.Аккумулятор Тогда
		СтрокаНаименование = "Аккумулятор ";
	ИначеЕсли ТипАгрегата = Справочники.уатТипыАгрегатов.Шина Тогда	
		СтрокаНаименование = "Шина ";
	ИначеЕсли ЗначениеЗаполнено(ТипАгрегата) тогда
		СтрокаНаименование = "" + ТипАгрегата.Наименование + " ";
	Иначе
		СтрокаНаименование = "Агрегат ";
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(СерийныйНомер) Тогда
		СтрокаНаименование = СтрокаНаименование + "с/н: " + СерийныйНомер + ", ";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Модель) Тогда
		СтрокаНаименование = СтрокаНаименование + Модель.Наименование + ", ";
	КонецЕсли;
	
	Если Прав(СтрокаНаименование, 2) = ", " Тогда
		СтрокаНаименование = Лев(СтрокаНаименование, СтрДлина(СтрокаНаименование) - 2);
	КонецЕсли;
	
	Если ПустаяСтрока(СтрокаНаименование) Тогда
		СтрокаНаименование = "<Свойства не назначены>";
	КонецЕсли;
	
	Возврат СтрокаНаименование;
КонецФункции

// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	уатОбщегоНазначения.уатПроверкаПравПередЗаписьюВСправочниках(ЭтотОбъект, Отказ, , Права);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

// сохранение ссылки на коллекцию прав, настроек и окружения
Права = Неопределено;
#Если Клиент Тогда
	Права = глПраваУАТ;
#КонецЕсли