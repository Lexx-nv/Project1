&НаКлиенте
Процедура УстановитьОтбор()
	флЭлементНайден = Ложь;
	Для Каждого ТекЭл Из Список.Отбор.Элементы Цикл
		Если ТекЭл.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец") Тогда
			флЭлементНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если НЕ флЭлементНайден Тогда
		ТекЭл = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ТекЭл.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец");
	КонецЕсли;
	ТекЭл.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ТекЭл.ПравоеЗначение = Организация;
	ТекЭл.Использование = Истина;
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовок()
	мРеквизитыОрганизации = ПолучитьРеквизитыПоСтроке(Организация, "Наименование, ЮрФизЛицо");
	Заголовок = "Список подразделений организации " + мРеквизитыОрганизации.Наименование;
	
	ЭтоЮрЛицо = (мРеквизитыОрганизации.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"));
	Если НЕ ЭтоЮрЛицо Тогда
		Элементы.КодПоОКАТО.Видимость = Ложь;
		Элементы.КПП.Видимость        = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьОтбор();
	ОбновитьЗаголовок();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Организация.Пустая() Тогда
		Организация = ПолучитьОсновнуюОрганизацию();
	КонецЕсли;
	УстановитьОтбор();
	ОбновитьЗаголовок();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыПоСтроке(пОбъект, пСтрокаСвойств)
	мСтрокаСвойств = СтрЗаменить(пСтрокаСвойств, ",", Символы.ПС);
	вСтруктура = Новый Структура;
	Для Итр = 1 По СтрЧислоСтрок(мСтрокаСвойств) Цикл
		мИмяРеквизита = СокрЛП(СтрПолучитьСтроку(мСтрокаСвойств, Итр));
		вСтруктура.Вставить(мИмяРеквизита, пОбъект[мИмяРеквизита]);
	КонецЦикла;
	Возврат вСтруктура;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОсновнуюОрганизацию()
	вОрганизация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	Если вОрганизация.Пустая() Тогда
		мВыборка = Справочники.Организации.Выбрать();
		мВыборка.Следующий();
		вОрганизация = мВыборка.Ссылка;
	КонецЕсли;
	
	Возврат вОрганизация;
КонецФункции