////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

&НаКлиенте
Перем мФормироватьНаименованиеАвтоматически;  // надо ли формировать автоматически Наименование, или нет


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция формирует наименование терминала.
//
&НаКлиенте
Функция СформироватьАвтоНаименование()
	
	Элементы.Наименование.СписокВыбора.Очистить();
	
	СтрокаНаименования = ?(Объект.Модель.Пустая(), Формат(Объект.Код, "ЧГ=0"), Строка(Объект.Модель)+" ("+Формат(Объект.Код, "ЧГ=0")+")");
	СтрокаНаименования = Лев(СтрокаНаименования, 50);
	
	Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
	
	Если НЕ Объект.Модель.Пустая() Тогда
		
		СтрокаНаименования2 = Формат(Объект.Код, "ЧГ=0")+ " ("+Строка(Объект.Модель)+")";
		СтрокаНаименования2 = Лев(СтрокаНаименования2, 50);
	
		Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования2);		
	
	КонецЕсли;
			
	Возврат СтрокаНаименования;

КонецФункции // СформироватьАвтоНаименование()

// Процедура проверяет, совпадало ли ранее наименование с кодом,
// и присваивает соответствующее значение переменной мФормироватьНаименованиеАвтоматически.
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура УстановитьФлагФормироватьНаименованиеАвтоматически()

	Если ПустаяСтрока(Объект.Наименование) 
	 ИЛИ Объект.Наименование = Формат(Объект.Код, "ЧГ=0")
	 ИЛИ Объект.Наименование = СформироватьАвтоНаименование() Тогда
		мФормироватьНаименованиеАвтоматически = Истина;
	Иначе
		мФормироватьНаименованиеАвтоматически = Ложь;
	КонецЕсли;

КонецПроцедуры // УстановитьФлагФормироватьНаименованиеАвтоматически()


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередЗаписью" формы.
//
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = СформироватьАвтоНаименование();
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбораИзСписка" поля "Наименование".
//
&НаКлиенте
Процедура НаименованиеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	СформироватьАвтоНаименование();
	ВыбранныйЭлемент = ВыбратьИзСписка(Элементы.Наименование.СписокВыбора, Элемент);
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Объект.Наименование = ВыбранныйЭлемент.Значение;
		мФормироватьНаименованиеАвтоматически = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "Код".
//
&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	Объект.Наименование = СформироватьАвтоНаименование();
	мФормироватьНаименованиеАвтоматически = Истина;
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "Наименование".
//
&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	УстановитьФлагФормироватьНаименованиеАвтоматически();
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля "Модель".
//
&НаКлиенте
Процедура МодельПриИзменении(Элемент)
	
	Объект.Наименование = СформироватьАвтоНаименование();
	мФормироватьНаименованиеАвтоматически = Истина;
	
КонецПроцедуры


