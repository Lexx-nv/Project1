////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем мВалютаРегламентированногоУчета Экспорт; // Валюта регламентированного учёта из настроек
Перем ЭтоВалютныйСчет Экспорт; //Признак валютного счета


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

//Функция проверяет корректность номера банковского счёта
//
// Параметры:
//  Номер - номер банковского счёта для проверки
//  ТекстОшибки -строка -в переменной формируется текст ошибки
// Возвращаемое значение:
//  Булево - корректность номера
//
Функция ПроверитьКорректностьНомераСчета(Номер, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(Номер) = Тип("Строка") Тогда
		ТекстОшибки = "Номер счета должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(Номер) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЭтоВалютныйСчет И СтрДлина(Номер) <> 20 Тогда
			ТекстОшибки = "Номер счета должен состоять из 20 знаков.";
			Возврат Ложь;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(Номер) Тогда
		ТекстОшибки = "В номере счета есть не только цифры. Возможно, номер указан неверно.";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьНомераСчета()

//Функция проверяет корректность  БИК банка
//
// Параметры:
//  Номер - БИК банка для проверки
//  ТекстОшибки -строка -в переменной формируется текст ошибки
// Возвращаемое значение:
//  Булево - корректность БИК банка
//
Функция ПроверитьКорректностьБИК(БИК, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(БИК) = Тип("Строка") Тогда
		ТекстОшибки = "БИК банка должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(БИК) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтрДлина(БИК) <> 9 Тогда
		ТекстОшибки = "БИК банка должен состоять из 9 знаков.";
		Возврат Ложь;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(БИК) Тогда
		ТекстОшибки = "В составе БИК банка должны быть только цифры.";
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ Лев(БИК, 2) = "04" Тогда
		ТекстОшибки = "Первые 2 цифры БИК банка должны быть ""04"".";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьБИК()

//Функция проверяет корректность  корр. счёта банка
//
// Параметры:
//  Коррсчет - номер  корр. счёта банка для проверки
//  ТекстОшибки -строка -в переменной формируется текст ошибки
// Возвращаемое значение:
//  Булево - корректность  корр. счёта банка
//
Функция ПроверитьКорректностьКоррсчета(Коррсчет, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(Коррсчет) = Тип("Строка") Тогда
		ТекстОшибки = "Корр.счет банка должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(Коррсчет) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтрДлина(Коррсчет) <> 20 Тогда
		ТекстОшибки = "Корр.счет банка должен состоять из 20 знаков.";
		Возврат Ложь;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(Коррсчет) Тогда
		ТекстОшибки = "В составе корр.счета банка должны быть только цифры.";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьКоррсчета()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления ИЛИ Ссылка.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = "Запись банковского счета (" + Владелец + ") " + Наименование + ", код " + Код;
	
	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(СокрЛП(НомерСчета)) Тогда 
		ОбщегоНазначения.СообщитьОбОшибке("В номере банковского счета присутствуют не только цифры. 
		|Возможно, номер указан неправильно", , Заголовок);
	КонецЕсли;

	// Проверка заполнения обязательных реквизитов
	Если НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбрана валюта.", Отказ, Заголовок);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ВидСчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбран вид счета.", Отказ, Заголовок);
	КонецЕсли;
	Если ВалютаДенежныхСредств = мВалютаРегламентированногоУчета Тогда
	
		Если НЕ ЗначениеЗаполнено(НомерСчета) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан номер счета.", Отказ, Заголовок);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Банк) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не выбран банк.", Отказ, Заголовок);
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();