// Функция проверяет, существуют ли ссылки на указанную модель ТС в регистрах движения ГСМ.
// Если есть - нельзя менять признак
//
// Параметры:
//  Ссылка       - Ссылка на проверяемую модель ТС
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СуществуютСсылкиНаНаличиеТопливногоБака(Ссылка) Экспорт

	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос();

	Запрос.УстановитьПараметр("Модель", Ссылка);

	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатОборотыПоЗаправкамГСМ.ТС
	|ИЗ
	|	РегистрНакопления.уатОборотыПоЗаправкамГСМ КАК уатОборотыПоЗаправкамГСМ
	|ГДЕ
	|	уатОборотыПоЗаправкамГСМ.ТС.Модель = &Модель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатОстаткиГСМнаТС.ТС
	|ИЗ
	|	РегистрНакопления.уатОстаткиГСМнаТС КАК уатОстаткиГСМнаТС
	|ГДЕ
	|	уатОстаткиГСМнаТС.ТС.Модель = &Модель";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции //  СуществуютСсылки()

// Функция проверяет, существуют ли ссылки на указанную модель ТС в регистрах учета счетчиков ТС.
// Если есть - нельзя менять признак
//
// Параметры:
//  Ссылка       - Ссылка на проверяемую модель ТС
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СуществуютСсылкиНаНаличиеСпидометра(Ссылка) Экспорт

	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Модель", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уатСчетчикиТССрезПоследних.ТС
	|ИЗ
	|	РегистрСведений.уатСчетчикиТС.СрезПоследних(, ТС.Модель = &Модель) КАК уатСчетчикиТССрезПоследних";
	
	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции //  СуществуютСсылки()

// Функция проверяет, существуют ли ссылки на указанную модель ТС в документах.
// Если есть - нельзя менять признак
//
// Параметры:
//  Ссылка       - Ссылка на проверяемую модель ТС
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СущестуютСсылкиНаПризнакАвтотранспорта(Ссылка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Модель", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатПутевойЛист.Ссылка
	|ИЗ
	|	Документ.уатПутевойЛист КАК уатПутевойЛист
	|ГДЕ
	|	уатПутевойЛист.ТранспортноеСредство.Модель = &Модель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатПутевойЛистПрицепы.Ссылка
	|ИЗ
	|	Документ.уатПутевойЛист.Прицепы КАК уатПутевойЛистПрицепы
	|ГДЕ
	|	уатПутевойЛистПрицепы.ТС.Модель = &Модель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатРазнарядкаРазнарядка.Ссылка
	|ИЗ
	|	Документ.уатРазнарядка.Разнарядка КАК уатРазнарядкаРазнарядка
	|ГДЕ
	|	уатРазнарядкаРазнарядка.ТС.Модель = &Модель";
	
	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции
