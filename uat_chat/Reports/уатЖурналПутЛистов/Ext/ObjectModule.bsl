#Если Клиент Тогда

Перем НП Экспорт; // Настройка периода отчета


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ОТЧЕТА

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//  ДокументРезультат - табличный документ, формируемый отчетом
//
Процедура СформироватьОтчет(ДокументРезультат) Экспорт
	
	Макет = ПолучитьМакет("Макет");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уатПутевойЛист.Номер,
	|	уатПутевойЛист.Водитель1 КАК Водитель,
	|	уатПутевойЛист.ТранспортноеСредство,
	|	уатПутевойЛист.Ссылка,
	|	уатПутевойЛист.ДатаВыезда КАК ДатаВыезда,
	|	уатСведенияОСотрудникахСрезПоследних.ТабельныйНомер
	|ИЗ
	|	Документ.уатПутевойЛист КАК уатПутевойЛист
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уатСведенияОСотрудниках.СрезПоследних(&ДатаКон, ) КАК уатСведенияОСотрудникахСрезПоследних
	|		ПО уатПутевойЛист.Водитель1 = уатСведенияОСотрудникахСрезПоследних.Сотрудник
	|ГДЕ
	|	уатПутевойЛист.ПометкаУдаления = &ПометкаУдаления
	|	И уатПутевойЛист.ДатаВыезда >= &ДатаНач
	|	И уатПутевойЛист.ДатаВыезда <= &ДатаКон
	|	И ВЫБОР
	|			КОГДА &ТСВыбыло
	|				ТОГДА уатПутевойЛист.ТранспортноеСредство.ДатаВыбытия <> ДАТАВРЕМЯ(1, 1, 1)
	|			КОГДА НЕ &ТСВыбыло
	|				ТОГДА уатПутевойЛист.ТранспортноеСредство.ДатаВыбытия = ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ";

	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Если ДатаКон = '00010101' Тогда
		Запрос.УстановитьПараметр("ДатаКон", '39991231');
	Иначе
		Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаКон));
	КонецЕсли;
	
	Если Не уатОбщегоНазначения.уатЗначениеНеЗаполнено(Организация) Тогда
		Запрос.Текст = Запрос.Текст +
		" И
		|	уатПутевойЛист.Организация = &Организация
		|";
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Если Не уатОбщегоНазначения.уатЗначениеНеЗаполнено(ВидПЛ) Тогда
		Запрос.Текст = Запрос.Текст +
		"
		|	И
		|	уатПутевойЛист.ВидПЛ = &ВидПЛ";
		Запрос.УстановитьПараметр("ВидПЛ", ВидПЛ);
	КонецЕсли;	

	Запрос.Текст = Запрос.Текст +
	"
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВыезда";
	
	Запрос.УстановитьПараметр("ТСВыбыло", ТСВыбыло);	
	
	Результат = Запрос.Выполнить();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьДетальныхЗаписей = Макет.ПолучитьОбласть("Детали");

	ДокументРезультат.Очистить();
	
	Если ПечататьОбложку Тогда
		ОбластьЗаголовок.Параметры.Организация = Организация.НаименованиеПолное;
		ДокументРезультат.Вывести(ОбластьЗаголовок);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;	
		
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
	ДокументРезультат.НачатьАвтогруппировкуСтрок();

	ВыборкаДетали = Результат.Выбрать();

	Пока ВыборкаДетали.Следующий() Цикл
		докПЛ = ВыборкаДетали.Ссылка;
		рсТранспортноеСредство = уатОбщегоНазначения.уатПрочитатьРеквизитыТС(ВыборкаДетали.ТранспортноеСредство);
		ОбластьДетальныхЗаписей.Параметры.ТранспортноеСредствоГарНомер	= рсТранспортноеСредство.ГаражныйНомер;
		ОбластьДетальныхЗаписей.Параметры.ВодительФИО	= уатОбщегоНазначенияТиповые.уатФамилияИнициалыФизЛица(ВыборкаДетали.Водитель);
		ОбластьДетальныхЗаписей.Параметры.ВодительТН	= ВыборкаДетали.ТабельныйНомер;
		ОбластьДетальныхЗаписей.Параметры.РасшифровкаТС	= ВыборкаДетали.ТранспортноеСредство;
		ОбластьДетальныхЗаписей.Параметры.РасшифровкаВодитель	= ВыборкаДетали.Водитель;
		ОбластьДетальныхЗаписей.Параметры.РасшифровкаПЛ	= ВыборкаДетали.Ссылка;

		ОбластьДетальныхЗаписей.Параметры.Заполнить(ВыборкаДетали);
		ДокументРезультат.Вывести(ОбластьДетальныхЗаписей, ВыборкаДетали.Уровень());
	КонецЦикла;

	ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ПовторятьПриПечатиСтроки = ОбластьШапкаТаблицы;
	ДокументРезультат.ТолькоПросмотр = уатПраваИНастройки.уатПраво("ЗащитаПечатныхФорм", глПраваУАТ);
			
КонецПроцедуры // СформироватьРезультат()


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

НП = Новый НастройкаПериода;

#КонецЕсли
