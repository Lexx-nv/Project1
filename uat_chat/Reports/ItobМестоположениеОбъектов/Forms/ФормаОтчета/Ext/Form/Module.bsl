
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура формирует отчет.
//
&НаСервере                                              
Процедура СформироватьОтчет()
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");	
	
	Если ОтчетОбъект.ПроверитьЗаполнение() Тогда
		
		Результат.Очистить();
		ОтчетОбъект.СкомпоноватьРезультат(Результат);
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ОтчетОбъект, "Отчет");
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	Результат.АвтоМасштаб = Истина;
	Результат.ИмяПараметровПечати = "Отчет_ItobМестоположениеОбъектов_ИмяПараметровПечати";
	
КонецПроцедуры // СформироватьОтчет()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "при создании на сервере" формы
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Константы.уатИспользуемаяСистемаGPS.Получить() <> Перечисления.уатСистемаGPS._1СЦСМ тогда
		#Если Клиент тогда
			ТекстСообщения = "Открытие возможно только тогда, когда используемая система GPS - 1C:Центр спутникового мониторинга.";
			ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения,Отказ);
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	СписокПараметров = Неопределено;
	
	Если Параметры.Свойство("СписокПараметров", СписокПараметров) Тогда
		
		СписокПараметров = Параметры.СписокПараметров;
		
		
		ОтчетМестоположениеОбъектов = РеквизитФормыВЗначение("Отчет");	
		Для каждого ЭлСписка Из СписокПараметров Цикл
			
			ОтчетМестоположениеОбъектов[ЭлСписка.Представление] = ЭлСписка.Значение; 
			
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(ОтчетМестоположениеОбъектов, "Отчет");
		
		СформироватьОтчет();
		
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура - обработчик события "ПриОткрытии" формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Отчет.Период) Тогда
		
		Отчет.Период = ТекущаяДата();
				
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик команды "Сформировать".
//
&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьОтчет();
	
КонецПроцедуры // Сформировать()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ДОКУМЕНТА

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда
	
		Оповестить("РежимМаршрута", Расшифровка);
	
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // ПолеТабличногоДокументаОбработкаРасшифровки()