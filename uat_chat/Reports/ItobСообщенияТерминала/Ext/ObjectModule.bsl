
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ 	

// Функция выполняет формирование табличного документа отчета.
//
Функция СформироватьОтчетСообщенияОтТерминала(НачПериода, КонПериода, Терминал)
			
	ТабДок = Новый ТабличныйДокумент;
			
	Макет = ПолучитьМакет("Макет");
	
	ОбластьШапка         = Макет.ПолучитьОбласть("Шапка|База");
	ОбластьДетали        = Макет.ПолучитьОбласть("Детали|База");
	ОбластьШапкаДатчик = Макет.ПолучитьОбласть("Шапка|Датчик");
	ОбластьДеталиДатчик = Макет.ПолучитьОбласть("Детали|Датчик");
		
	ОбластьШапка.Параметры.ЗаголовокОтчета = "Сообщения от терминала за "+ПредставлениеПериода(НачПериода, КонПериода);
	ОбластьШапка.Параметры.Объект = "Терминал: "+Терминал;	
	ТабДок.Вывести(ОбластьШапка);
	
	ПараметрыСдвигаВремени = ItobОперативныйМониторинг.ПолучитьПараметрыСдвигаВремени();
	
	ЗапросВидыДатчиков = Новый Запрос( "ВЫБРАТЬ
	                                   |	ItobДанныеДатчиков.Датчик КАК Датчик,
	                                   |	ItobДанныеДатчиков.Датчик.Код КАК ДатчикКод
	                                   |ИЗ
	                                   |	РегистрСведений.ItobДанныеДатчиков КАК ItobДанныеДатчиков
	                                   |ГДЕ
	                                   |	ItobДанныеДатчиков.Период МЕЖДУ &НачДата И &КонДата
	                                   |	И ItobДанныеДатчиков.Терминал = &Терминал
	                                   |
	                                   |СГРУППИРОВАТЬ ПО
	                                   |	ItobДанныеДатчиков.Датчик,
	                                   |	ItobДанныеДатчиков.Датчик.Код
	                                   |
	                                   |УПОРЯДОЧИТЬ ПО
	                                   |	ДатчикКод");
	ЗапросВидыДатчиков.УстановитьПараметр("Терминал", Терминал);
	ЗапросВидыДатчиков.УстановитьПараметр("НачДата", ItobОперативныйМониторинг.ПривестиКДатеВремениПоГринвичу(НачПериода));
	ЗапросВидыДатчиков.УстановитьПараметр("КонДата", ItobОперативныйМониторинг.ПривестиКДатеВремениПоГринвичу(КонПериода));
	
	ВыборкаВидыДатчиков = ЗапросВидыДатчиков.Выполнить().Выбрать();
	ШиринаТаб0 = ТабДок.ШиринаТаблицы;
	
	ДанныеДатчиковПоля = "";
	ДанныеДатчиковСоединения = "";
	
	ТекСчетчикДатчиков = 0;
	МассивПолейДатчиков = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Терминал", Терминал);
	Запрос.УстановитьПараметр("НачДата", ItobОперативныйМониторинг.ПривестиКДатеВремениПоГринвичу(НачПериода));
	Запрос.УстановитьПараметр("КонДата", ItobОперативныйМониторинг.ПривестиКДатеВремениПоГринвичу(КонПериода));
	Запрос.УстановитьПараметр("СдвигВремени", ПараметрыСдвигаВремени.СдвигВремени);
	Запрос.УстановитьПараметр("ВариантПереводаНаЛетнееВремя", ПараметрыСдвигаВремени.ВариантПереводаВремени);
	Запрос.УстановитьПараметр("СдвигЛетнееВремя", ПараметрыСдвигаВремени.СдвигЛетнееВремя);
	
	Пока ВыборкаВидыДатчиков.Следующий() Цикл
		ОбластьШапкаДатчик.Параметры.Датчик = ВыборкаВидыДатчиков.Датчик;
		ТабДок.Присоединить(ОбластьШапкаДатчик);	
		
		АлиасТаблицы = "ДанныеДатчиков"+Формат(ТекСчетчикДатчиков,"ЧН=0; ЧГ=0");
		АлиасПоля = "Значение"+Формат(ТекСчетчикДатчиков,"ЧН=0; ЧГ=0");
		АлиасОтбора = "Датчик"+Формат(ТекСчетчикДатчиков,"ЧН=0; ЧГ=0");
		
		ДанныеДатчиковПоля = ?(ДанныеДатчиковПоля="",","+Символы.ПС,ДанныеДатчиковПоля+","+Символы.ПС)
			+"ЕСТЬNULL("+АлиасТаблицы+".Значение,""--"") КАК "+АлиасПоля;
			
		ДанныеДатчиковСоединения = ?(ДанныеДатчиковСоединения="","",ДанныеДатчиковСоединения+""+Символы.ПС)
			+"ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ItobДанныеДатчиков КАК "+АлиасТаблицы+" ПО ItobДанныеТерминалов.Период = "+АлиасТаблицы+".Период "
			+" И "+АлиасТаблицы+".Датчик = &"+АлиасОтбора+" И "+АлиасТаблицы+".Терминал = &Терминал";
			
		Запрос.УстановитьПараметр(АлиасОтбора, ВыборкаВидыДатчиков.Датчик);
		МассивПолейДатчиков.Добавить(АлиасПоля);
			
	    ТекСчетчикДатчиков = ТекСчетчикДатчиков+1;
	КонецЦикла;
	
	Если ТекСчетчикДатчиков > 0 Тогда
		
		ОблЗаголовокДатчики = ТабДок.Область(ТабДок.ВысотаТаблицы-1, ШиринаТаб0+1, ТабДок.ВысотаТаблицы-1, ТабДок.ШиринаТаблицы);
		ОблЗаголовокДатчики.Объединить();
		ОблЗаголовокДатчики.Текст = "Датчики";	
		
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ	РАЗРЕШЕННЫЕ			   
				   |	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(ItobДанныеТерминалов.Период, СЕКУНДА, &СдвигВремени), СЕКУНДА, ВЫБОР
				   |			КОГДА &ВариантПереводаНаЛетнееВремя = ЗНАЧЕНИЕ(Перечисление.ItobВариантыПереводаВремени.Европейский)
				   |				ТОГДА ВЫБОР
				   |						КОГДА ItobДанныеТерминалов.Период МЕЖДУ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ItobДанныеТерминалов.Период, ГОД), МЕСЯЦ, 3), НЕДЕЛЯ), НЕДЕЛЯ, -1), ДЕНЬ), ЧАС, 2) И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ItobДанныеТерминалов.Период, ГОД), МЕСЯЦ, 10), НЕДЕЛЯ), НЕДЕЛЯ, -1), ДЕНЬ), ЧАС, 2)
				   |							ТОГДА &СдвигЛетнееВремя
				   |						ИНАЧЕ 0
				   |					КОНЕЦ
				   |			КОГДА &ВариантПереводаНаЛетнееВремя = ЗНАЧЕНИЕ(Перечисление.ItobВариантыПереводаВремени.Американский)
				   |				ТОГДА ВЫБОР
				   |						КОГДА ItobДанныеТерминалов.Период МЕЖДУ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ItobДанныеТерминалов.Период, ГОД), МЕСЯЦ, 2), НЕДЕЛЯ), НЕДЕЛЯ, 1), ДЕНЬ), ЧАС, 2) И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ItobДанныеТерминалов.Период, ГОД), МЕСЯЦ, 10), НЕДЕЛЯ), НЕДЕЛЯ, 0), ДЕНЬ), ЧАС, 2)
				   |							ТОГДА &СдвигЛетнееВремя
				   |						ИНАЧЕ 0
				   |					КОНЕЦ
				   |			ИНАЧЕ 0
				   |		КОНЕЦ) КАК Период,
				   |	ItobДанныеТерминалов.Широта КАК Широта,
				   |	ItobДанныеТерминалов.Долгота КАК Долгота,
				   |	ItobДанныеТерминалов.Скорость КАК Скорость,
				   |	ItobДанныеТерминалов.Направление КАК Направление
				   |	"+ДанныеДатчиковПоля+"
				   |ИЗ
				   |	РегистрСведений.ItobДанныеТерминалов КАК ItobДанныеТерминалов
				   |	"+ДанныеДатчиковСоединения+"
				   |ГДЕ
				   |	ItobДанныеТерминалов.Период МЕЖДУ &НачДата И &КонДата
				   |	И ItobДанныеТерминалов.Терминал = &Терминал
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	Период";
				   
	НомПП = 1;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
					
		ОбластьДетали.Параметры.Заполнить(Выборка);	
		ОбластьДетали.Параметры.НомПП = НомПП;
		ТабДок.Вывести(ОбластьДетали);
		
		Для каждого ТекАлиасПоля Из МассивПолейДатчиков Цикл
			ОбластьДеталиДатчик.Параметры.Значение = Выборка[ТекАлиасПоля];
			ТабДок.Присоединить(ОбластьДеталиДатчик);				
		
		КонецЦикла;
		
		НомПП = НомПП+1;
	
	КонецЦикла;
	
	Возврат ТабДок;

	
КонецФункции // СформироватьОтчетСообщенияОтТерминала()
  
// Процедура обработчик события "ПриКомпоновкеРезультата" объекта
//
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	
	ДокументРезультат.Вывести(СформироватьОтчетСообщенияОтТерминала(
		НачПериода,
		КонПериода,
		Терминал));
				
КонецПроцедуры // ПриКомпоновкеРезультата()
 
// Процедура - обработчик события "ОбработкаПроверкиЗаполнения".
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НачПериода > КонПериода Тогда
		ТекстОшибки = НСтр("ru='Начало периода не может быть больше даты конца периода'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			Неопределено, // ОбъектИлиСсылка
			"ItobСообщенияТерминала",
			"Отчет", // ПутьКДанным
			Отказ
		);
	КонецЕсли;
		
КонецПроцедуры // ОбработкаПроверкиЗаполнения()
