#Если Клиент Тогда
	
//=================================================================================
// ПРОЦЕДУРЫ И ФУНКЦИИ НАЧАЛЬНОЙ НАСТРОЙКИ ОТЧЕТА


// Процедура установки начальных настроек отчета с использованием текста запроса
//
Процедура УстановитьНачальныеНастройки(ДополнительныеПараметры = Неопределено) Экспорт
	
	// Содержит название отчета, которое будет выводиться в шапке.
	// Тип: Строка.
	// Пример:
	// УниверсальныйОтчет.мНазваниеОтчета = "Название отчета";
	УниверсальныйОтчет.мНазваниеОтчета = СокрЛП(ЭтотОбъект.Метаданные().Синоним);
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		//уатОбщегоНазначения.уатВосстановитьРеквизитыОтчета(ЭтотОбъект, ДополнительныеПараметры);
		
	КонецЕсли;
	
	УниверсальныйОтчет.мВыбиратьИмяРегистра = Ложь;
	УниверсальныйОтчет.мВыбиратьИспользованиеСвойств = Ложь;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ТС КАК ТС,
	|	ВложенныйЗапрос.Водитель КАК Водитель,
	|	ВложенныйЗапрос.ГСМ КАК ГСМ,
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ПериодДень КАК ПериодДень,
	|	ВложенныйЗапрос.ПериодНеделя КАК ПериодНеделя,
	|	ВложенныйЗапрос.ПериодДекада КАК ПериодДекада,
	|	ВложенныйЗапрос.ПериодМесяц КАК ПериодМесяц,
	|	ВложенныйЗапрос.ПериодКвартал КАК ПериодКвартал,
	|	ВложенныйЗапрос.ПериодПолугодие КАК ПериодПолугодие,
	|	ВложенныйЗапрос.ПериодГод КАК ПериодГод,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.РасходПоНормеОборот КАК РасходПоНормеОборот,
	|	ВложенныйЗапрос.РасходПоФактуОборот КАК РасходПоФактуОборот,
	|	ВложенныйЗапрос.Экономия КАК Экономия,
	|	ВложенныйЗапрос.Пережог КАК Пережог,
	|	ВложенныйЗапрос.ТСГарНомер КАК ТСГарНомер,
	|	ВложенныйЗапрос.ТСГосНомер КАК ТСГосНомер,
	|	ВложенныйЗапрос.ЭкономияПроцент КАК ЭкономияПроцент,
	|	ВложенныйЗапрос.ПережогПроцент КАК ПережогПроцент,
	|	ВложенныйЗапрос.ГруппаГСМ КАК ГруппаГСМ
	|{ВЫБРАТЬ
	|	ТС.*,
	|	Водитель.*,
	|	ГСМ.*,
	|	Период,
	|	ПериодДень,
	|	ПериодНеделя,
	|	ПериодДекада,
	|	ПериодМесяц,
	|	ПериодКвартал,
	|	ПериодПолугодие,
	|	ПериодГод,
	|	Регистратор.*,
	|	РасходПоНормеОборот,
	|	РасходПоФактуОборот,
	|	Экономия,
	|	Пережог,
	|	ТСГарНомер,
	|	ТСГосНомер,
	|	ЭкономияПроцент,
	|	ПережогПроцент,
	|	ГруппаГСМ.*}
	|ИЗ
	|	(ВЫБРАТЬ
	|		уатРасходГСМнаТСОбороты.Период КАК Период,
	|		НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, ДЕНЬ) КАК ПериодДень,
	|		НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, НЕДЕЛЯ) КАК ПериодНеделя,
	|		НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, ДЕКАДА) КАК ПериодДекада,
	|		НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, МЕСЯЦ) КАК ПериодМесяц,
	|		НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, КВАРТАЛ) КАК ПериодКвартал,
	|		НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, ПОЛУГОДИЕ) КАК ПериодПолугодие,
	|		НАЧАЛОПЕРИОДА(уатРасходГСМнаТСОбороты.Период, ГОД) КАК ПериодГод,
	|		уатРасходГСМнаТСОбороты.Регистратор КАК Регистратор,
	|		уатРасходГСМнаТСОбороты.ТС КАК ТС,
	|		уатРасходГСМнаТСОбороты.Водитель КАК Водитель,
	|		уатРасходГСМнаТСОбороты.ГСМ КАК ГСМ,
	|		уатРасходГСМнаТСОбороты.РасходПоНормеОборот КАК РасходПоНормеОборот,
	|		уатРасходГСМнаТСОбороты.РасходПоФактуОборот КАК РасходПоФактуОборот,
	|		ВЫБОР
	|			КОГДА уатРасходГСМнаТСОбороты.РасходПоНормеОборот > уатРасходГСМнаТСОбороты.РасходПоФактуОборот
	|				ТОГДА уатРасходГСМнаТСОбороты.РасходПоНормеОборот - уатРасходГСМнаТСОбороты.РасходПоФактуОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Экономия,
	|		ВЫБОР
	|			КОГДА уатРасходГСМнаТСОбороты.РасходПоНормеОборот < уатРасходГСМнаТСОбороты.РасходПоФактуОборот
	|				ТОГДА уатРасходГСМнаТСОбороты.РасходПоФактуОборот - уатРасходГСМнаТСОбороты.РасходПоНормеОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Пережог,
	|		уатРасходГСМнаТСОбороты.ТС.ГаражныйНомер КАК ТСГарНомер,
	|		уатРасходГСМнаТСОбороты.ТС.ГосударственныйНомер КАК ТСГосНомер,
	|		ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА уатРасходГСМнаТСОбороты.РасходПоНормеОборот > уатРасходГСМнаТСОбороты.РасходПоФактуОборот
	|					ТОГДА ВЫБОР
	|							КОГДА уатРасходГСМнаТСОбороты.РасходПоНормеОборот = 0
	|								ТОГДА 0
	|							ИНАЧЕ (уатРасходГСМнаТСОбороты.РасходПоНормеОборот - уатРасходГСМнаТСОбороты.РасходПоФактуОборот) / уатРасходГСМнаТСОбороты.РасходПоНормеОборот * 100
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК ЧИСЛО(15, 1)) КАК ЭкономияПроцент,
	|		ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА уатРасходГСМнаТСОбороты.РасходПоНормеОборот < уатРасходГСМнаТСОбороты.РасходПоФактуОборот
	|					ТОГДА ВЫБОР
	|							КОГДА уатРасходГСМнаТСОбороты.РасходПоНормеОборот = 0
	|								ТОГДА 0
	|							ИНАЧЕ (уатРасходГСМнаТСОбороты.РасходПоФактуОборот - уатРасходГСМнаТСОбороты.РасходПоНормеОборот) / уатРасходГСМнаТСОбороты.РасходПоНормеОборот * 100
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК ЧИСЛО(15, 1)) КАК ПережогПроцент,
	|		уатНоменклатураГСМ.ГруппаГСМ.Ссылка КАК ГруппаГСМ
	|	ИЗ
	|		РегистрНакопления.уатРасходГСМнаТС.Обороты(&ДатаНач, &ДатаКон, Регистратор, ) КАК уатРасходГСМнаТСОбороты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уатНоменклатураГСМ КАК уатНоменклатураГСМ
	|			ПО уатРасходГСМнаТСОбороты.ГСМ = уатНоменклатураГСМ.Номенклатура
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		уатРасходТехническихЖидкостейОбороты.Период,
	|		НАЧАЛОПЕРИОДА(уатРасходТехническихЖидкостейОбороты.Период, ДЕНЬ),
	|		НАЧАЛОПЕРИОДА(уатРасходТехническихЖидкостейОбороты.Период, НЕДЕЛЯ),
	|		НАЧАЛОПЕРИОДА(уатРасходТехническихЖидкостейОбороты.Период, ДЕКАДА),
	|		НАЧАЛОПЕРИОДА(уатРасходТехническихЖидкостейОбороты.Период, МЕСЯЦ),
	|		НАЧАЛОПЕРИОДА(уатРасходТехническихЖидкостейОбороты.Период, КВАРТАЛ),
	|		НАЧАЛОПЕРИОДА(уатРасходТехническихЖидкостейОбороты.Период, ПОЛУГОДИЕ),
	|		НАЧАЛОПЕРИОДА(уатРасходТехническихЖидкостейОбороты.Период, ГОД),
	|		уатРасходТехническихЖидкостейОбороты.Регистратор,
	|		уатРасходТехническихЖидкостейОбороты.ТС,
	|		уатРасходТехническихЖидкостейОбороты.Водитель,
	|		уатРасходТехническихЖидкостейОбороты.ГСМ,
	|		NULL,
	|		уатРасходТехническихЖидкостейОбороты.КоличествоРасходОборот,
	|		NULL,
	|		NULL,
	|		уатРасходТехническихЖидкостейОбороты.ТС.ГаражныйНомер,
	|		уатРасходТехническихЖидкостейОбороты.ТС.ГосударственныйНомер,
	|		NULL,
	|		NULL,
	|		уатНоменклатураГСМ.ГруппаГСМ.Ссылка
	|	ИЗ
	|		РегистрНакопления.уатРасходТехническихЖидкостей.Обороты(&ДатаНач, &ДатаКон, Регистратор, ) КАК уатРасходТехническихЖидкостейОбороты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уатНоменклатураГСМ КАК уатНоменклатураГСМ
	|			ПО уатРасходТехническихЖидкостейОбороты.ГСМ = уатНоменклатураГСМ.Номенклатура) КАК ВложенныйЗапрос
	|{ГДЕ
	|	ВложенныйЗапрос.ТС.*,
	|	ВложенныйЗапрос.Водитель.*,
	|	ВложенныйЗапрос.ГСМ.*,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ПериодДень,
	|	ВложенныйЗапрос.ПериодНеделя,
	|	ВложенныйЗапрос.ПериодДекада,
	|	ВложенныйЗапрос.ПериодМесяц,
	|	ВложенныйЗапрос.ПериодКвартал,
	|	ВложенныйЗапрос.ПериодПолугодие,
	|	ВложенныйЗапрос.ПериодГод,
	|	ВложенныйЗапрос.Регистратор.*,
	|	ВложенныйЗапрос.ТСГарНомер,
	|	ВложенныйЗапрос.ТСГосНомер,
	|	ВложенныйЗапрос.ГруппаГСМ.*,
	|	ВложенныйЗапрос.РасходПоНормеОборот,
	|	ВложенныйЗапрос.РасходПоФактуОборот,
	|	ВложенныйЗапрос.Экономия,
	|	ВложенныйЗапрос.Пережог,
	|	ВложенныйЗапрос.ЭкономияПроцент,
	|	ВложенныйЗапрос.ПережогПроцент,
	|	(ВЫБОР
	|			КОГДА ВложенныйЗапрос.ТС.ДатаВыбытия <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			КОГДА ВложенныйЗапрос.ТС.ДатаВыбытия = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЛОЖЬ
	|		КОНЕЦ) КАК ТСВыбыло}
	|{УПОРЯДОЧИТЬ ПО
	|	ТС.*,
	|	Водитель.*,
	|	ГСМ.*,
	|	Период,
	|	ПериодДень,
	|	ПериодНеделя,
	|	ПериодДекада,
	|	ПериодМесяц,
	|	ПериодКвартал,
	|	ПериодПолугодие,
	|	ПериодГод,
	|	Регистратор.*,
	|	РасходПоНормеОборот,
	|	РасходПоФактуОборот,
	|	Экономия,
	|	Пережог,
	|	ТСГарНомер,
	|	ТСГосНомер,
	|	ЭкономияПроцент,
	|	ПережогПроцент,
	|	ГруппаГСМ.*}
	|ИТОГИ
	|	СУММА(РасходПоНормеОборот),
	|	СУММА(РасходПоФактуОборот),
	|	ВЫБОР
	|		КОГДА СУММА(РасходПоНормеОборот) > СУММА(РасходПоФактуОборот)
	|			ТОГДА СУММА(РасходПоНормеОборот) - СУММА(РасходПоФактуОборот)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Экономия,
	|	ВЫБОР
	|		КОГДА СУММА(РасходПоНормеОборот) < СУММА(РасходПоФактуОборот)
	|			ТОГДА СУММА(РасходПоФактуОборот) - СУММА(РасходПоНормеОборот)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Пережог,
	|	ВЫБОР
	|		КОГДА СУММА(РасходПоНормеОборот) > СУММА(РасходПоФактуОборот)
	|			ТОГДА ВЫБОР
	|					КОГДА СУММА(РасходПоНормеОборот) = 0
	|						ТОГДА 0
	|					ИНАЧЕ ВЫРАЗИТЬ((СУММА(РасходПоНормеОборот) - СУММА(РасходПоФактуОборот)) / СУММА(РасходПоНормеОборот) * 100 КАК ЧИСЛО(15, 2))
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЭкономияПроцент,
	|	ВЫБОР
	|		КОГДА СУММА(РасходПоНормеОборот) < СУММА(РасходПоФактуОборот)
	|			ТОГДА ВЫБОР
	|					КОГДА СУММА(РасходПоНормеОборот) = 0
	|						ТОГДА 0
	|					ИНАЧЕ ВЫРАЗИТЬ((СУММА(РасходПоФактуОборот) - СУММА(РасходПоНормеОборот)) / СУММА(РасходПоНормеОборот) * 100 КАК ЧИСЛО(15, 2))
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПережогПроцент
	|ПО
	|	ТС,
	|	ГСМ,
	|	Водитель,
	|	ТСГосНомер,
	|	ГруппаГСМ,
	|	ПериодКвартал,
	|	ТСГарНомер,
	|	ПериодПолугодие,
	|	ПериодНеделя,
	|	ПериодДень,
	|	Период,
	|	Регистратор,
	|	ПериодДекада,
	|	ПериодМесяц,
	|	ПериодГод
	|{ИТОГИ ПО
	|	ТС.*,
	|	Водитель.*,
	|	ГСМ.*,
	|	Период,
	|	ПериодДень,
	|	ПериодНеделя,
	|	ПериодДекада,
	|	ПериодМесяц,
	|	ПериодКвартал,
	|	ПериодПолугодие,
	|	ПериодГод,
	|	Регистратор.*,
	|	ТСГарНомер,
	|	ТСГосНомер,
	|	ГруппаГСМ.*}";
	
	
	// В универсальном отчете включен флаг использования свойств и категорий.
	//Если УниверсальныйОтчет.ИспользоватьСвойстваИКатегории Тогда
	//	
	//	// Добавление свойств и категорий поля запроса в таблицу полей.
	//	// Необходимо вызывать для каждого поля запроса, предоставляющего возможность использования свойств и категорий.
	//	
	//	// УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля(<ПсевдонимТаблицы>.<Поле> , 
	//                                                  <ПсевдонимПоля>, <Представление>, <Назначение>);
	//	УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("уатДТП.Заказчик", "Заказчик", "Заказчик", 
	//                                ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Контрагенты);
	//	
	//	// Добавление свойств и категорий в исходный текст запроса.
	//	УниверсальныйОтчет.ДобавитьВТекстЗапросаСвойстваИКатегории(ТекстЗапроса);
	//	
	//КонецЕсли;
	
	// Инициализация текста запроса построителя отчета
	УниверсальныйОтчет.ПостроительОтчета.Текст = ТекстЗапроса;
	
	Пока УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки.Количество() > 0 Цикл
		
		УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки.Удалить(УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки[0]);
		
	КонецЦикла;
	
	
	// Представления полей отчета.
	// Необходимо вызывать для каждого поля запроса.
	// УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить(<ИмяПоля>, <ПредставлениеПоля>);
	
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ТС"			             , "ТС / оборудование (все поля)");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ТСГарНомер"			     , "ТС (гар. номер)");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ТСГосНомер"      			 , "ТС (гос. номер)");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ГруппаГСМ"       			 , "Вид ГСМ");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("РасходПоНормеОборот"       , "Расход по норме");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("РасходПоФактуОборот"       , "Расход по факту");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ЭкономияПроцент"           , "Экономия %");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ПережогПроцент"            , "Пережог %");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ТСВыбыло"					 , "ТС выбыло");
	
	УниверсальныйОтчет.УстановитьПредставленияПолей(УниверсальныйОтчет.мСтруктураПредставлениеПолей, УниверсальныйОтчет.ПостроительОтчета);
	
	//// Заполнение начальных настроек универсального отчета
	//УниверсальныйОтчет.УстановитьНачальныеНастройки(Истина);
	
	// Добавление предопределенных группировок строк отчета.
	// Необходимо вызывать для каждой добавляемой группировки строки.
	// УниверсальныйОтчет.ДобавитьИзмерениеСтроки(<ПутьКДанным>);
	
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ТС");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ГруппаГСМ");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("ГСМ");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Водитель");
	
	УниверсальныйОтчет.ДобавитьПоказатель("РасходПоНормеОборот", "Расход по норме", Истина,"ЧДЦ=3", , );
	УниверсальныйОтчет.ДобавитьПоказатель("РасходПоФактуОборот", "Расход по факту", Истина,"ЧДЦ=3", , );
	УниверсальныйОтчет.ДобавитьПоказатель("Экономия"           , "Экономия"       , Истина,"ЧДЦ=3", , );
	УниверсальныйОтчет.ДобавитьПоказатель("Пережог"            , "Пережог"        , Истина,"ЧДЦ=3", , );
	УниверсальныйОтчет.ДобавитьПоказатель("ЭкономияПроцент"    , "Экономия %"     , Истина,"ЧДЦ=1", , );
	УниверсальныйОтчет.ДобавитьПоказатель("ПережогПроцент"     , "Пережог %"      , Истина,"ЧДЦ=1", , );

	// Добавление предопределенных отборов отчета.
	// Необходимо вызывать для каждого добавляемого отбора.
	УниверсальныйОтчет.ДобавитьОтбор("ТСГосНомер", Ложь  , ВидСравнения.Содержит);
	УниверсальныйОтчет.ДобавитьОтбор("ГСМ");
	УниверсальныйОтчет.ДобавитьОтбор("Водитель");
	УниверсальныйОтчет.ДобавитьОтбор("ГруппаГСМ" , Истина, ВидСравнения.Равно, Перечисления.уатГруппыГСМ.Топливо);
	УниверсальныйОтчет.ДобавитьОтбор("ТСВыбыло"  , Истина, ВидСравнения.Равно, Ложь);
	
	// Установка связи подчиненных и родительских полей
	// УниверсальныйОтчет.УстановитьСвязьПолей(<ПутьКДанным>, <ПутьКДанным>);
	
	// Установка связи полей и измерений
	// УниверсальныйОтчет.УстановитьСвязьПоляИИзмерения(<ИмяПоля>, <ИмяИзмерения>);
	//УниверсальныйОтчет.УстановитьСвязьПоляИИзмерения("ВыполнениеЗаказовНаТС", "ЗаказНаТС");	
	
	// Заполнение начальных настроек универсального отчета
	УниверсальныйОтчет.УстановитьНачальныеНастройки(Ложь);
	
	// Добавление дополнительных полей
	// Необходимо вызывать для каждого добавляемого дополнительного поля.
	УниверсальныйОтчет.ДобавитьДополнительноеПоле("ТСГосНомер"      , ТипРазмещенияРеквизитовИзмерений.Отдельно);

	//ПутьКДанным, Размещение = Неопределено, Положение = 3
	
КонецПроцедуры // УстановитьНачальныеНастройки()

//=================================================================================
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ОТЧЕТА 

// Процедура формирования отчета
//
Процедура СформироватьОтчет(ТабличныйДокумент) Экспорт
	
	УниверсальныйОтчет.СформироватьОтчет(ТабличныйДокумент);
	ТабличныйДокумент.ТолькоПросмотр = уатПраваИНастройки.уатПраво("ЗащитаПечатныхФорм", глПраваУАТ);
	
КонецПроцедуры // СформироватьОтчет()

//=================================================================================
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура обработки расшифровки
//
Процедура ОбработкаРасшифровки(Расшифровка, Объект) Экспорт
	
	// Дополнительные параметры в расшифровывающий отчет можно передать
	// посредством инициализации переменной "ДополнительныеПараметры".
	
	ДополнительныеПараметры = уатОбщегоНазначения.уатСохранитьРеквизитыОтчета(ЭтотОбъект);
	УниверсальныйОтчет.ОбработкаРасшифровкиУниверсальногоОтчета(Расшифровка, Объект, ДополнительныеПараметры);
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру для сохранения настроек отчета
//
Процедура СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками) Экспорт
	
	УниверсальныйОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками);
	уатОбщегоНазначения.уатСохранитьРеквизитыОтчета(ЭтотОбъект, СтруктураСНастройками);
	
КонецПроцедуры // СформироватьСтруктуруДляСохраненияНастроек()

// Заполняет настройки отчета из структуры сохраненных настроек
//
Функция ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками) Экспорт
	
	уатОбщегоНазначения.уатВосстановитьРеквизитыОтчета(ЭтотОбъект, СтруктураСНастройками);
	Возврат УниверсальныйОтчет.ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками, ЭтотОбъект);
	
КонецФункции // ВосстановитьНастройкиИзСтруктуры()

// Содержит значение используемого режима ввода периода.
// Тип: Число.
// Возможные значения: 0 - произвольный период, 1 - на дату, 2 - неделя, 3 - декада, 
// 4 - месяц, 5 - квартал, 6 - полугодие, 7 - год
// Значение по умолчанию: 0
// Пример:
УниверсальныйОтчет.мРежимВводаПериода = 0;
	
#КонецЕсли
